// SPDX-FileCopyrightText: Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package main

import os

fun main(){
	if os.ARGS.contains('--ghci-win') {
		link_github_actions()
		return
	}

	platform := os.platform()
	if platform == 'linux' {
		link_linux()
		return
	}

	if platform == 'windows' {
		link_windows()
		println('Please restart your terminal to use bait')
		return
	}

	eprintln('Unsupported platform ${platform}')
	exit(1)
}

fun link_linux() {
	link_dir := '/usr/local/bin'
	os.mkdir_all(link_dir)
	link_path := link_dir + '/bait'
	if os.exists(link_path) {
		os.rm(link_path)
	}
	exe_path := os.abs_path($BAITEXE.replace('.js', ''))
	os.symlink(exe_path, link_path)
}

fun link_windows() {
	link_dir := os.join_path($BAITDIR, ['bin'])
	if os.exists(link_dir) {
		os.rmdir_all(link_dir)
	}
	os.mkdir_all(link_dir)

	link_js := os.join_path(link_dir, ['bait.js'])
	orig_js := $BAITEXE
	os.symlink(orig_js, link_js)

	link_bat := os.join_path(link_dir, ['bait.bat'])
	orig_bat := $BAITEXE.replace('.js', '.bat')
	os.symlink(orig_bat, link_bat)

	cur_path := os.getenv('PATH')
	res := os.exec('setx /M path "${cur_path};${link_dir}"')
	if res.code != 0 {
		eprintln(res.stderr)
		exit(1)
	}
}

fun link_github_actions() {
	mut path_content := os.read_file(os.getenv('GITHUB_PATH'))
	path_content += '\n${os.getwd()}\n'
	os.write_file(os.getenv('GITHUB_PATH'), path_content)
}
