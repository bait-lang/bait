// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package main

import os
import bait.preference
import bait.ast
import bait.lexer
import bait.parser

const PRINT_TOKENS := os.ARGS.contains('--tokens')

fun main() {
	args := os.user_args()
	if args.length == 0 {
		eprintln('error: please specify a file to print the ast')
		exit(1)
	}
	path := args[0]
	if not path.ends_with('.bt') {
		eprintln('error: file must have ".bt" extension')
		exit(1)
	}

	text := os.read_file(path)

	if PRINT_TOKENS {
		mut l := lexer.Lexer{}
		l.init(text, .discard)
		mut tok := l.next()
		for tok != .eof {
			println(if tok.has_val() { '${tok}: ${l.val()}' } else { tok.str() })
			tok = l.next()
		}
		return
	}

	mut p := parser.new_default()
	p.init(text, path, '')
	file_ast := p.parse_result() or {
		p.warnings.print()
		p.to_error(err).print()
		exit(1)
	}

	println(file_ast)
}
