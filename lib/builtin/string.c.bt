// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0
package builtin

import strings

struct string {
pub mut:
	str &u8
	length i32
}

fun (s string) get(i i32) u8 {
	return s.str[i]
}

pub fun (s string) substr(start i32, end i32) string {
	length := end - start
	mut res := string{
		str = #C.calloc(1, length + 1)
		length = length
	}
	#C.memcpy(res.str, #C.'s.str + start', length)
	res.str[length] = 0 as u8
	return res
}

pub fun (s string) index(search string) i32 {
	if search.length == 0 {
		return -1
	}
	return s.index_kmp(search)
	return -1
}

pub fun (s string) index_last(search string) i32 {
	if search.length == 0 or search.length > s.length {
		return -1
	}

	for i := s.length - search.length; i >= 0; i -= 1 {
		mut j := 0
		for j < search.length and s.str[i + j] == search.str[j] {
			j += 1
		}
		if j == search.length {
			return i
		}
	}

	return -1
}

fun (pattern string) kmp_prefix_table() []i32 {
	mut prefix := []i32{length = pattern.length}
	mut j := 0

	for i, c in pattern {
		for j > 0 and pattern.str[j] != c {
			j = prefix[j - i]
		}
		if pattern.str[j] == c {
			j += 1
		}
		prefix[i] = j
	}

	return prefix
}

fun (s string) index_kmp(search string) i32 {
	if search.length > s.length {
		return -1
	}

	prefix := search.kmp_prefix_table()
	mut j := 0

	for i, c in s {
		for j > 0 and search.str[j] != c {
			j = prefix[j - 1]
		}
		if search.str[j] == c {
			j += 1
		}
		if j == search.length {
			return i - search.length + 1
		}
	}

	return -1
}

pub fun (s string) starts_with(prefix string) bool {
	for i := 0; i < prefix.length; i += 1 {
		if s.str[i] != prefix.str[i] {
			return false
		}
	}

	return true
}

pub fun (s string) ends_with(suffix string) bool {
	offset := s.length - suffix.length
	for i := 0; i < suffix.length; i += 1 {
		if s.str[offset + i] != suffix.str[i] {
			return false
		}
	}

	return true
}

pub fun (s string) repeat(count i32) string {
	if count == 0 {
		return ''
	}

	if count == 1 {
		return s
	}

	mut buf := #C.calloc(1, s.length * count + 1)
	for i := 0; i < count; i += 1 {
		#C.memcpy(#C.'buf + i * s.length', s.str, s.length)
	}

	new_len := s.length * count
	buf[new_len] = 0 as u8

	return string{
		str = buf
		length = new_len
	}
}

@overload: '+'
fun (a string) add (b string) string {
	new_len := a.length + b.length
	res := string{
		str = #C.calloc(1, new_len + 1)
		length = new_len
	}
	for i:=0; i < a.length; i+=1 {
		res.str[i] = a.str[i]
	}
	for i:=0; i < b.length; i+=1 {
		res.str[a.length + i] = b.str[i]
	}
	res.str[new_len] = 0 as u8
	return res
}

@overload: '=='
fun (a string) eq (b string) bool {
	if a.length != b.length {
		return false
	}

	for i := 0; i < a.length; i += 1 {
		if a.str[i] != b.str[i] {
			return false
		}
	}
	return true
}

pub fun from_c_string(cs &u8) string {
	return string{
		str = cs
		length = #C.strlen(cs)
	}
}

fun interpolate(strs []string) string {
	mut b := strings.new_builder(100)
	for s in strs {
		b.write(s)
	}
	return b.str()
}
