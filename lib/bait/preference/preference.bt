// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package preference

import os
import bait.errors

pub enum Backend {
	js
	c
}

pub enum OperatingSystem {
	linux
	windows
}

pub struct Prefs {
	global command string
	global out_name string

	pub mut args []string
	pub mut user_args []string
	pub mut build_options []string

	pub mut should_run bool
	pub mut is_verbose bool
	pub mut show_timings bool
	pub mut backend Backend
	pub mut os OperatingSystem

	// Compile mode
	pub mut is_test bool
	pub mut is_script bool
	pub mut is_library bool
	pub mut keep_exe bool
	pub mut cc string := 'cc'

	// Error related
	pub mut hide_warnings bool
	pub mut warn_is_error bool
}

pub fun parse_args(args []string) Prefs {
	mut p := Prefs{
		backend = .js
		os = os_from_string(os.platform())
	}
	// Default action in case of no arguments: help
	if args.length == 0 {
		p.command = 'help'
		return p
	}

	mut after_double_dash := false

	// Parse options and commands
	for i := 0; i < args.length; i += 1{
		arg := args[i]
		match arg {
			'-b', '--backend' {
				i += 1
				p.backend = backend_from_string(args[i])
				p.build_options.push(arg)
				p.build_options.push(args[i])
			}
			'-os', '--os' {
				i += 1
				p.os = os_from_string(args[i])
				p.build_options.push(arg)
				p.build_options.push(args[i])
			}
			"-o", '--out' {
				i += 1
				p.out_name = args[i]
				p.build_options.push(arg)
				p.build_options.push(p.out_name)
			}
			'-cc', '--cc' {
				i += 1
				p.cc = args[i]
				p.build_options.push(arg)
				p.build_options.push(p.out_name)
			}
			'-v', '--verbose' {
				p.is_verbose = true
				p.build_options.push(arg)
			}
			'-k', '--keep' {
				p.keep_exe = true
			}
			'--library' {
				p.is_library = true
			}
			'-w' {
				p.hide_warnings = true
				p.build_options.push(arg)
			}
			'-W' {
				p.warn_is_error = true
				p.build_options.push(arg)
			}
			'--nocolor' {
				os.setenv('BAITCOLOR', '0')
			}
			'--script' {
				p.is_script = true
			}
			'--timings' {
				p.show_timings = true
			}
			'--' {
				after_double_dash = true
			}
			else{
				if p.command.length == 0 {
					p.command = arg
				} else if after_double_dash {
					p.user_args.push(arg)
				} else {
					p.args.push(arg)
				}
			}
		}
	}

	p.build_options.push_many(p.args)

	if p.hide_warnings and p.warn_is_error {
		errors.generic_error('Cannot use -w and -W together.')
		exit(1)
	}

	// `build` is just an optional alias and replaced with the filepath
	if p.command == 'build' {
		p.command = p.args[0]
	}
	// `run` is the same but with `should_run` set to true
	if p.command == 'run' {
		p.command = p.args[0]
		p.should_run = true
	}

	if p.command == 'test' {
		p.should_run = true
		p.keep_exe = true
		p.is_test = true
	}

	// Set default out name if empty
	if p.out_name.length == 0 {
		mut ext := ''
		if p.backend == .js {
			ext = '.js'
		}

		if p.command.ends_with('.bt') {
			p.out_name = p.command.replace('.bt', ext)
		} else {
			p.out_name = os.abs_path(p.command).all_after_last(os.PATH_SEP) + ext
		}
	}

	return p
}

fun backend_from_string(s string) Backend {
	return match s {
		'js' { Backend.js }
		'c' { .c }
		else { panic('Invalid backend: ${s}') }
	}
}

fun os_from_string(s string) OperatingSystem {
	return match s {
		'linux' { OperatingSystem.linux }
		'win', 'windows' { .windows }
		else { panic('Invalid OS: ${s}') }
	}
}
