// Copyright (c) 2023-present Lukas Neubert and contributors (see AUTHORS.md).
// This Source Code is subject to the terms of the Mozilla Public License 2.0.
package preference

import os

pub enum Backend {
	js
	c
}

pub struct Prefs {
	command string
	args []string
	out_name string
	should_run bool
	is_verbose bool
	is_test bool
	is_script bool
	is_library bool
	backend Backend
	// Values for compile time pseudo variables
	baitexe string
	baitdir string
	baithash string
}

pub fun parse_args(args []string) Prefs {
	p := Prefs{
		backend: .js
	}
	// Default action in case of no arguments: help
	if args.length == 0 {
		p.command = 'help'
		return p
	}

	// Parse options and commands
	for i := 0; i < args.length; i += 1{
		arg := args[i]
		match arg {
			'-b', '--backend' {
				i += 1
				p.backend = backend_from_string(args[i])
			}
			"-o", '--out' {
				i += 1
				p.out_name = args[i]
			}
			'-v', '--verbose' {
				p.is_verbose = true
				p.args.push(arg) // keep option for tools
			}
			'--library' {
				p.is_library = true
			}
			'--nocolor' {
				os.setenv('BAITCOLOR', '0')
			}
			'--script' {
				p.is_script = true
			}
			else{
				if p.command.length == 0 {
					p.command = arg
				} else {
					p.args.push(arg)
				}
			}
		}
	}

	// `build` is just an optional alias and replaced with the filepath
	if p.command == 'build' {
		p.command = p.args[0]
	}
	// `run` is the same but with `should_run` set to true
	if p.command == 'run' {
		p.command = p.args[0]
		p.should_run = true
	}

	if p.command == 'test' {
		p.is_test = true
	}
	if p.out_name.length == 0 {
		p.out_name = p.command.replace('.bt', '.js')
	}
	return p
}

pub fun (p Prefs) set_comptime_vars() {
	p.baitexe = os.executable()
	p.baitdir = os.dir(p.baitexe)
	p.baithash = os.exec('git rev-parse --short HEAD').stdout.trim_space()
}

fun backend_from_string(s string) Backend {
	match s {
		'js' { return .js }
		'c' { return .c }
	}
	// TODO error
	return .js
}
