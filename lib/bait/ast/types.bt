// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package ast

type Type := i32

pub const PLACEHOLDER_TYPE := new_type(0)
pub const ERROR_TYPE := new_type(1)
pub const VOID_TYPE := new_type(2)
pub const I8_TYPE := new_type(3)
pub const I16_TYPE := new_type(4)
pub const I32_TYPE := new_type(5)
pub const I64_TYPE := new_type(6)
pub const U8_TYPE := new_type(7)
pub const U16_TYPE := new_type(8)
pub const U32_TYPE := new_type(9)
pub const U64_TYPE := new_type(10)
pub const F32_TYPE := new_type(11)
pub const F64_TYPE := new_type(12)
pub const BOOL_TYPE := new_type(13)
pub const STRING_TYPE := new_type(14)
pub const ARRAY_TYPE := new_type(15)
pub const MAP_TYPE := new_type(16)
pub const ANY_TYPE := new_type(17)
pub const USER_TYPES_SPACE := 18

pub fun new_type(t i32) Type {
	return t as Type
}

pub fun (t Type) idx() i32 {
	return (t & 0xffff) as i32
}

pub fun (t Type) set_nr_amp(n i32) Type {
	return new_type((t & 0xff00ffff | n << 16) as i32)
}

pub fun (t Type) get_nr_amp() i32 {
	return (t >> 16 & 0xff) as i32
}

pub fun (t Type) is_int() bool {
	return t >= I8_TYPE and t <= U64_TYPE
}

const BUILTIN_STRUCT_TYPES := [
	STRING_TYPE,
	ARRAY_TYPE,
	MAP_TYPE
]

pub enum TypeKind {
	other
	placeholder
	number
	string
	array
	map
	interface_
	struct_
	enum_
	alias_type
	sum_type
	fun_
	generic
	generic_inst
	result
}

pub struct TypeSymbol{
	pub mut name string
	pub mut kind TypeKind
	pub mut methods []FunDecl
	pub mut parent Type
	pub mut overloads map[string]FunDecl
	pub mut is_pub bool
	pub mut pkg string
	global info TypeInfo := EmptyInfo{}
}

pub fun (sym TypeSymbol) find_field(name string, t Table) StructField {
	if sym.kind == .generic_inst {
		return t.unwrap_generic(sym).find_field(name, t)
	}

	if sym.info is StructInfo {
		info := sym.info as StructInfo
		for f in info.fields {
			if f.name == name {
				return f
			}
		}
	}
	if BUILTIN_STRUCT_TYPES.contains(sym.parent) {
		parent_sym := t.get_sym(sym.parent)
		return parent_sym.find_field(name, t)
	}

	return StructField{
		typ = ERROR_TYPE
	}
}

pub fun (sym TypeSymbol) has_method(name string) bool {
	for m in sym.methods {
		if m.name == name {
			return true
		}
	}
	return false
}

pub fun (t Table) register_builtins(){
	// IMPORTANT: Order of registration must match the order of the constants above
	_ = t.register_sym(TypeSymbol{ name = '_' }) // PLACEHOLDER_TYPE
	_ = t.register_sym(TypeSymbol{ name = '_err' }) // ERROR_TYPE
	_ = t.register_sym(TypeSymbol{ name = 'void' })
	_ = t.register_num('i8')
	_ = t.register_num('i16')
	_ = t.register_num('i32')
	_ = t.register_num('i64')
	_ = t.register_num('u8')
	_ = t.register_num('u16')
	_ = t.register_num('u32')
	_ = t.register_num('u64')
	_ = t.register_sym(TypeSymbol{ name = 'f32' })
	_ = t.register_sym(TypeSymbol{ name = 'f64' })
	_ = t.register_sym(TypeSymbol{ name = 'bool' })
	_ = t.register_sym(TypeSymbol{
		name = 'string'
		kind = .string
	})
	_ = t.register_sym(TypeSymbol{
		name = 'Array'
		kind = .array
	})
	_ = t.register_sym(TypeSymbol{
		name = 'Map'
		kind = .map
	})
	_ = t.register_sym(TypeSymbol{ name = 'any'})
}

pub fun (t Table) register_num(name string) {
	_ = t.register_sym(TypeSymbol{
		name = name
		kind = .number
		is_pub = true
	})
}
