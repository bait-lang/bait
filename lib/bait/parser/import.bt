// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package parser

import bait.ast
import bait.token

fun (p Parser) add_builtin_imports() []ast.Import {
	if p.pkg_name == 'builtin' {
		return []
	}

	// Everything implicitly imports builtin
	mut imports := []ast.Import
	imports.push(ast.Import{
		name = 'builtin'
		alias = 'builtin'
	})

	// Internal package test
	if p.pref.is_test and p.pkg_name != 'main' {
		imports.push(ast.Import{
			name = p.pkg_name
		})
	}

	return imports
}

// `import 'name' as #JS.name`
fun (mut p Parser) foreign_import(pos token.Pos) !ast.Import {
	name := p.val
	p.next()
	p.check(.key_as)!

	lang := p.parse_lang()!

	mut alias := ''
	if lang == .js {
		alias = lang.prepend_to(p.check_name()!)
	} else if lang == .c {
		_ = p.check_name()!
	}

	return ast.Import{
		name = name
		alias = alias
		lang = lang
		pos = pos
	}
}

fun (mut p Parser) bait_import(pos token.Pos) !ast.Import {
	// Import name, e.g. `bait.gen.c`
	mut name_parts := [p.check_name()!]
	for p.tok == .dot {
		p.next()
		name_parts.push(p.check_name()!)
	}
	name := name_parts.join('.')

	// Optional alias, e.g. `as cgen`
	mut alias := name_parts.last()
	if p.tok == .key_as {
		p.next()
		alias = p.check_name()!
	}

	return ast.Import{
		name = name
		alias = alias
		pos = pos
	}
}

fun (mut p Parser) import_stmts() ![]ast.Import{
	mut imports := p.add_builtin_imports()

	for p.tok == .key_import {
		pos := p.pos
		p.next()

		mut imp := if p.tok == .string { p.foreign_import(pos)! } else { p.bait_import(pos)! }

		p.import_aliases[imp.alias] = imp.name
		imports.push(imp)
	}

	return imports
}
