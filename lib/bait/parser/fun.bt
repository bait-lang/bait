// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package parser

import bait.ast
import bait.context

fun (mut p Parser) fun_decl() !ast.FunDecl{
	pos := p.pos
	is_pub := p.check_pub()
	p.check(.key_fun)!

	mut lang := ast.Language.bait
	mut is_method := false
	mut params := []ast.Param

	// Method receiver
	if p.tok == .lpar {
		is_method = true
		p.next()

		mut is_mut := false
		if p.tok == .key_mut {
			is_mut = true
			p.next()
		}

		// Pass mutable receiver as pointer
		rec_name := p.check_name()!

		mut rec_type := p.parse_type()!
		if is_mut {
			rec_type = rec_type.set_nr_amp(1)
		}

		params.push(ast.Param{
			is_mut = is_mut
			name = rec_name
			typ = rec_type
		})
		p.check(.rpar)!
	}

	ffi := p.parse_ffi_pkg()!
	lang = ffi.lang
	mut key := ""
	if lang != .bait {
		key = ffi.pkg + "." // TMP
	}

	// normal function name
	name := p.check_name()!
	key += name
	is_test := name.starts_with('test_')
	if is_method {
		key = '${params[0].typ}.${name}'
	} else if lang == .bait {
		key = p.prepend_pkg(key)
	}

	// Generics
	generic_names := p.generic_type_names()!

	// Params
	p.check(.lpar)!
	params.push_many(p.fun_params()!)
	p.check(.rpar)!

	mut return_type := ast.VOID_TYPE
	if p.tok != .lcur and pos.line == p.pos.line {
		return_type = p.parse_type()!
	}

	mut node := ast.FunDecl{
		is_test = is_test
		is_pub = is_pub
		name = name
		key = key
		generic_names = generic_names
		params = params
		return_type = return_type
		noreturn = p.attributes.has('noreturn')
		attrs = p.attributes
		lang = lang
		pos = pos
	}

	// Register method or function
	if is_method {
		sym := p.table.get_sym(params[0].typ)
		// TODO move this error into checker
		if lang == .bait and sym.has_method(name) {
			p.error('Method "${name}" already exists on type "${sym.name}"')!
		}
		sym.methods.push(node)
	} else {
		mut param_types := []ast.Type
		for param in params {
			param_types.push(param.typ)
		}
		node.typ = p.table.find_or_register_fun(param_types, return_type, false)

		if lang == .bait {
			p.pkg_scope.register_unique(name, context.ScopeObject{
				kind = .function
				typ = node.typ
				pkg = p.pkg_name
				is_pub = is_pub
				noreturn = p.attributes.has('noreturn')
				return_type = return_type
				params = params
				attrs = p.attributes
				generic_names = generic_names
				key = key
			})
		} else {
			ffi_scope := p.sema_ctx.obtain_root_scope(ffi.pkg)
			ffi_scope.register(name, context.ScopeObject{
				kind = .function
				is_pub = true
				return_type = return_type
				params = params
				key = key
			})
		}
	}

	p.attributes = []ast.Attribute

	if lang == .bait {
		node.stmts = p.parse_block()!
	}

	node.is_method = is_method
	return node
}

fun (mut p Parser) anon_fun() !ast.AnonFun{
	pos := p.pos
	p.check(.key_fun)!
	p.check(.lpar)!
	params := p.fun_params()!
	p.check(.rpar)!
	mut return_type := ast.VOID_TYPE
	if p.tok != .lcur {
		return_type = p.parse_type()!
	}
	mut param_types := []ast.Type
	for param in params {
		param_types.push(param.typ)
	}
	typ := p.table.find_or_register_fun(param_types, return_type, true)
	stmts := p.parse_block()!
	return ast.AnonFun{
		decl = ast.FunDecl{
			name = '_anon_${p.file_hash}_${p.lexer.offset()}'
			params = params
			return_type = return_type
			stmts = stmts
		}
		typ = typ
		pos = pos
	}
}

fun (mut p Parser) fun_params() ![]ast.Param{
	mut params := []ast.Param
	for p.tok != .rpar{
		pos := p.pos
		mut is_mut := false
		if p.tok == .key_mut {
			is_mut = true
			p.next()
		}
		param := ast.Param{
			is_mut = is_mut
			name = p.check_name()!
			typ = p.parse_type()!
			pos = pos
		}
		params.push(param)
		if p.tok != .rpar{
			p.check(.comma)!
		}
	}
	return params
}

fun (mut p Parser) fun_call(lang ast.Language) !ast.CallExpr{
	pos := p.pos
	name := p.check_name()!
	pkg := p.get_expr_pkg()
	p.check(.lpar)!
	args := p.call_args()!
	p.check(.rpar)!

	or_block := p.or_block()!

	return ast.CallExpr{
		pkg = pkg
		name = name
		args = args
		or_block = or_block
		pos = pos
		lang = lang
	}
}

fun (mut p Parser) method_call(left ast.Expr) !ast.CallExpr {
	pos := p.pos
	name := p.check_name()!
	p.check(.lpar)!
	args := p.call_args()!
	p.check(.rpar)!

	or_block := p.or_block()!

	return ast.CallExpr{
		is_method = true
		left = left
		name = name
		args = args
		or_block = or_block
		pos = pos
	}
}

fun (mut p Parser) call_args() ![]ast.CallArg {
	mut args := []ast.CallArg
	for p.tok != .rpar {
		is_mut := p.tok == .key_mut
		args.push(ast.CallArg{
			is_mut = is_mut // FIXME: gen mixes up order of code here and puts result before
			expr = p.expr(.lowest)!
		})
		if p.tok != .rpar {
			if p.tok == .eof {
				break
			}
			p.check(.comma)!
		}
	}
	return args
}

fun (mut p Parser) or_block() !ast.OrBlock {
	// Propagation
	if p.tok == .excl {
		p.next()
		return ast.OrBlock{
			kind = .prop
		}
	}

	// Block `or {`
	if p.tok == .key_or and p.peek() == .lcur {
		p.next()
		stmts := p.parse_block()!

		return ast.OrBlock {
			kind = .block
			stmts = stmts
		}
	}

	// No block
	return ast.OrBlock{
		kind = .none
	}
}
