// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0
package parser

import bait.ast
import bait.token

fun (mut p Parser) expr(precedence token.Precedence) ast.Expr {
	mut node := p.single_expr()
	for precedence < p.tok.precedence() {
		if p.tok == .dot {
			node = p.dot_expr(node)
		} else if p.tok == .lsqr {
			node = p.index_expr(node)
		} else if p.tok == .key_as {
			node = p.as_cast(node)
		} else if p.tok.is_infix() {
			node = p.infix_expr(node)
		} else {
			return node
		}
	}
	return node
}

fun (mut p Parser) single_expr() ast.Expr {
	match p.tok {
		.hash {
			return p.hash_expr()
		}
		.char {
			return p.char_literal()
		}
		.dollar {
			return p.comptime_expr()
		}
		.dot {
			return p.enum_val(false)
		}
		.float {
			return p.float_literal()
		}
		.integer {
			return p.integer_literal()
		}
		.lsqr {
			return p.array_init()
		}
		.lpar {
			return p.par_expr()
		}
		.name {
			return p.name_expr(.bait)
		}
		.string {
			return p.string_literal(.bait)
		}
		.key_fun {
			return p.anon_fun()
		}
		.key_false, .key_true {
			return p.bool_literal()
		}
		.key_if {
			return p.if_match(true)
		}
		.key_match {
			return p.match_as_if_expr(true)
		}
		.key_mut {
			return p.ident(.bait)
		}
		.key_not, .minus, .mul, .tilde {
			return p.prefix_expr()
		}
		.key_typeof {
			return p.typeof_expr()
		}
		.eof {
			p.should_abort = true
			return ast.EmptyExpr{} as ast.Expr
		}
		.error {
			p.error(p.val)
			return ast.EmptyExpr{} as ast.Expr
		}
		else {
			mut msg := 'invalid expression: kind = ${p.tok}'
			if p.val.length > 0 {
				msg += ', val = ${p.val}'
			}
			p.error(msg)
			return ast.EmptyExpr{} as ast.Expr
		}
	}
}

fun (mut p Parser) expr_list() []ast.Expr{
	mut exprs := []ast.Expr
	for true {
		exprs.push(p.expr(.lowest))
		if p.tok != .comma {
			break
		}
		p.check(.comma)
	}
	return exprs
}

fun (mut p Parser) array_init()ast.ArrayInit{
	pos := p.pos
	p.check(.lsqr)

	// Type init, e.g. `[]i32`
	if p.tok == .rsqr {
		p.next()
		elem_type := p.parse_type()
		typ := p.table.find_or_register_array(elem_type)

		// Optional parameters, e.g. `[]i32{ length = 10 cap = 20 }`
		mut length_expr := ast.EmptyExpr{} as ast.Expr
		mut cap_expr := ast.EmptyExpr{} as ast.Expr
		if p.tok == .lcur {
			p.next()
			for p.tok != .rcur {
				key := p.check_name()
				p.check(.assign)
				expr := p.expr(.lowest)

				if key == 'length' {
					length_expr = expr
				} else if key == 'cap' {
					cap_expr = expr
				} else {
					p.error('invalid array init field: ${key}')
				}
			}
			p.check(.rcur)
		}

		return ast.ArrayInit{
			typ = typ
			elem_type = elem_type
			length_expr = length_expr
			cap_expr = cap_expr
			pos = pos
		}
	}

	// Expressions init, e.g. `[1, 2, 3]`
	mut exprs := []ast.Expr
	for p.tok != .rsqr {
		exprs.push(p.expr(.lowest))
		if p.tok == .comma {
			p.next()
		}
	}
	p.check(.rsqr)
	p.skip(.semicolon)
	return ast.ArrayInit{
		exprs = exprs
		pos = pos
	}
}

fun (mut p Parser) as_cast(left ast.Expr) ast.AsCast {
	pos := p.pos
	p.check(.key_as)
	target := p.parse_type()
	return ast.AsCast{
		target = target
		expr = left
		pos = pos
	}
}

fun (mut p Parser) bool_literal()ast.BoolLiteral{
	pos := p.pos
	val := p.tok == .key_true
	p.next()
	return ast.BoolLiteral{
		val = val
		pos = pos
	}
}

fun (mut p Parser) char_literal()ast.CharLiteral{
	pos := p.pos
	val := p.val
	p.next()
	return ast.CharLiteral{
		val = val
		pos = pos
	}
}

fun (mut p Parser) comptime_expr() ast.Expr {
	if p.peek() == .key_if {
		return p.if_match(false)
	}

	return p.comptime_var()
}

fun (mut p Parser) comptime_var() ast.ComptimeVar{
	pos := p.pos
	p.next()
	name := p.check_name()
	return ast.ComptimeVar{
		name = name
		pos = pos
	}
}

fun (mut p Parser) dot_expr(left ast.Expr) ast.Expr {
	pos := p.pos
	p.check(.dot)
	if p.peek() == .lpar {
		return p.method_call(left)
	}

	name := p.check_name()
	return ast.SelectorExpr{
		expr = left
		field_name = name
		pos = pos
	}
}

fun (mut p Parser) enum_val(has_name bool) ast.EnumVal{
	pos := p.pos
	mut name := ''
	if has_name {
		name = p.check_name()
		if p.expr_pkg.length > 0 {
			name = p.prepend_expr_pkg(name)
		} else {
			name = p.prepend_pkg(name)
		}
	}
	p.check(.dot)
	val := p.check_name()
	return ast.EnumVal{
		name = name
		val = val
		pos = pos
	}
}

fun (mut p Parser) hash_expr()ast.Expr{
	lang := p.parse_lang()
	if p.tok != .string {
		if lang == .js {
			p.expr_pkg = 'JS'
		} else if lang == .c {
			p.expr_pkg = 'C'
		}
		return p.name_expr(lang)
	}

	str_node := p.string_literal(.bait) as ast.StringLiteral
	return ast.HashExpr{
		lang = lang
		val = str_node.val
		pos = str_node.pos
	}
}

fun (mut p Parser) ident(lang ast.Language)ast.Ident{
	pos := p.pos
	mut is_mut := p.tok == .key_mut
	if is_mut {
		p.next()
	}
	if p.is_for_init {
		is_mut = true
	}
	name := p.prepend_expr_pkg(p.check_name())
	return ast.Ident{
		name = name
		is_mut = is_mut
		pkg = p.pkg_name
		pos = pos
		lang = lang
	}
}

fun (mut p Parser) index_expr(left ast.Expr)ast.IndexExpr {
	pos := p.pos
	p.check(.lsqr)
	index := p.expr(.lowest)
	p.check(.rsqr)
	p.skip(.semicolon)
	return ast.IndexExpr{
		left = left
		index = index
		pos = pos
	}
}

fun (mut p Parser) infix_expr(left ast.Expr)ast.InfixExpr{
	pos := p.pos
	op_tok := p.tok
	p.next()
	right := p.expr(op_tok.precedence())
	return ast.InfixExpr{
		left = left
		right = right
		op = op_tok
		pos = pos
	}
}

fun (mut p Parser) map_init() ast.MapInit{
	pos := p.pos
	p.next()
	if p.tok == .lsqr{
		map_type := p.parse_map_type()
		return ast.MapInit{
			typ = map_type
			pos = pos
		}
	}
	mut keys := []ast.Expr
	mut vals := []ast.Expr
	p.check(.lcur)
	for p.tok != .rcur {
		keys.push(p.expr(.lowest))
		p.check(.colon)
		vals.push(p.expr(.lowest))
	}
	p.next()
	return ast.MapInit{
		keys = keys
		vals = vals
		pos = pos
	}
}

fun (mut p Parser) name_expr(lang ast.Language) ast.Expr{
	if p.peek() == .dot {
		if lang == .js {
			p.expr_pkg += '.' + p.check_name()
			p.check(.dot)
		} else if p.import_aliases.contains(p.val) {
			p.expr_pkg = p.import_aliases[p.val]
			p.next()
			p.next()
		}
	}
	if p.val == 'map' {
		return p.map_init()
	}

	if p.peek() == .lpar {
		return p.fun_call(lang)
	}

	capitalised := p.val[0].is_upper() and not p.val.is_upper()

	if p.is_struct_possible and (p.peek() == .lcur or p.peek() == .lsqr) {
		if capitalised or ['array', 'map', 'string'].contains(p.val) {
			return p.struct_init()
		}
	}

	if capitalised and p.peek() == .dot {
		return p.enum_val(true)
	}

	if p.peek() == .string and p.val == 'js' {
		p.next()
		return p.string_literal(.js)
	}

	return p.ident(lang)
}

fun (mut p Parser) float_literal() ast.FloatLiteral {
	n := ast.FloatLiteral{
		val = p.val
		pos = p.pos
	}

	p.next()
	return n
}

fun (mut p Parser) integer_literal() ast.IntegerLiteral {
	n := ast.IntegerLiteral{
		val = p.val
		pos = p.pos
	}

	p.next()
	return n
}

fun (mut p Parser) par_expr() ast.ParExpr {
	pos := p.pos
	p.next()
	expr := p.expr(.lowest)
	p.check(.rpar)
	return ast.ParExpr{
		expr = expr
		pos = pos
	}
}

fun (mut p Parser) prefix_expr()ast.PrefixExpr{
	pos := p.pos
	op := p.tok
	p.next()
	right := p.expr(.prefix)
	return ast.PrefixExpr{
		op = op
		right = right
		pos = pos
	}
}

fun (mut p Parser) string_literal(lang ast.Language)ast.Expr{
	pos := p.pos
	if p.peek() != .str_dollar {
		val := p.val
		p.next()
		return ast.StringLiteral{
			lang = lang
			val = val
			pos = pos
		}
	}

	// Interpolated string
	mut vals := []string
	mut exprs := []ast.Expr
	for p.tok == .string {
		vals.push(p.val)
		p.next()
		if p.tok != .str_dollar {
			break
		}
		p.next()
		p.check(.lcur)
		exprs.push(p.expr(.lowest))
	}
	return ast.StringInterLiteral{
		vals = vals
		exprs = exprs
		pos = pos
	}
}

fun (mut p Parser) typeof_expr() ast.TypeOf {
	pos := p.pos
	p.check(.key_typeof)
	p.check(.lpar)
	expr := p.expr(.lowest)
	p.check(.rpar)
	return ast.TypeOf{
		expr = expr
		pos = pos
	}
}
