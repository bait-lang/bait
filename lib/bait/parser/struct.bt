// SPDX-FileCopyrightText: Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package parser

import bait.ast
import bait.context
import bait.token

fun (mut p Parser) struct_decl() !ast.StructDecl{
	pos := p.pos
	is_pub := p.check_pub()
	p.check(.key_struct)!

	lang := p.parse_lang()!
	mut name := p.check_name()!
	if lang != .bait {
		name = lang.prepend_to(name)
	}

	generic_names := p.generic_type_names()!

	mut fields := []ast.StructField

	p.check(.lcur)!
	for p.tok != .rcur {
		f_is_pub := p.tok == .key_pub
		if f_is_pub {
			p.next()
		}
		f_is_mut := p.tok == .key_mut
		if f_is_mut {
			p.next()
		}
		f_is_global := p.tok == .key_global
		if f_is_global {
			if f_is_pub or f_is_mut {
				return error('unexpected `global`')
			}
			p.next()
		}

		p.parse_attributes()!
		fields.push(p.struct_decl_field(f_is_mut, f_is_pub, f_is_global)!)
	}
	p.check(.rcur)!

	tsym := ast.TypeSymbol{
		kind = .struct_
		name = p.prepend_pkg(name)
		is_pub = is_pub
		pkg = p.pkg_name
		info = ast.StructInfo{
			fields = fields
			generic_names = generic_names
		}
	}
	typ := p.table.register_sym(tsym)
	p.pkg_scope.register_unique(name, context.ScopeObject{
		kind = .type_
		typ = typ
	})

	return ast.StructDecl{
		lang = lang
		pkg_prefix = p.prepend_pkg('')
		name = name
		typ = typ
		fields = fields
		generic_names = generic_names
		pos = pos
	}
}

fun (mut p Parser) struct_decl_field(is_mut bool, is_pub bool, is_global bool) !ast.StructField {
	pos := p.pos

	fname := p.check_name()!
	ftyp := p.parse_type()!
	mut expr := ast.InvalidExpr{} as ast.Expr
	if p.tok == .decl_assign {
		p.next()
		expr = p.expr(.lowest)!
	}
	field := ast.StructField{
		name = fname
		typ = ftyp
		expr = expr
		pos = pos
		is_mut = is_mut or is_global
		is_pub = is_pub or is_global
		is_global = is_global
		attrs = p.attributes
	}

	p.attributes = []ast.Attribute
	return field
}

fun (mut p Parser) struct_init() !ast.StructInit{
	pos := p.pos

	name := p.val
	mut typ := p.parse_type()!
	conc_types := p.concrete_types()!
	if conc_types.length > 0 {
		parent := p.table.get_sym(typ)
		typ = p.table.register_sym(ast.TypeSymbol{
			kind = .generic_inst
			parent = typ
			pkg = parent.pkg
			info = ast.GenericInst{
				concrete_types = conc_types
			}
		})
	}

	p.check(.lcur)!
	mut fields := []ast.StructInitField
	for p.tok != .rcur {
		fields.push(p.struct_init_field()!)
	}
	p.check(.rcur)!

	return ast.StructInit{
		name = name
		typ = typ
		fields = fields
		pos = pos
		concrete_types = conc_types
	}
}

fun (mut p Parser) struct_init_field() !ast.StructInitField{
	pos := p.pos

	name := p.check_name()!
	p.check(.assign)!
	expr := p.expr(.lowest)!

	return ast.StructInitField{
		name = name
		expr = expr
		pos = pos
	}
}
