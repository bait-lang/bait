// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0
package parser

import os
import bait.preference
import bait.errors
import bait.ast
import bait.lexer
import bait.token

pub struct Parser {
	pref preference.Prefs
	path string
	table ast.Table
	lexer lexer.Lexer
	// tokens []token.Token
mut:
	eofs i32
	idx i32
	tok token.TokenKind
	next_tok token.TokenKind
	val string
	pkg_name string
	import_aliases map[string]string
	attributes []ast.Attribute
	expr_pkg string
	is_for_init bool
	is_struct_possible bool
	should_abort bool
pub mut:
	warnings []errors.Message
	errors []errors.Message
}

pub fun parse(text string, path string, table ast.Table, pref preference.Prefs) ast.File {
	mut p := Parser{
		lexer = lexer.Lexer{}
		pref = pref
		path = path
		table = table
		is_struct_possible = true
	}

	p.lexer.init(text)

	// p.next()
	p.next()

	pkg_decl := p.package_decl()

	// Validate the files package name actually matches the desired import
	if p.pref.expected_pkg.length > 0 and pkg_decl.full_name != p.pref.expected_pkg {
		return ast.File{}
	}

	// Do not parse assert.bt outside of tests
	if not p.pref.is_test and pkg_decl.name == 'builtin' and path.ends_with('assert.bt') {
		return ast.File{
			path = path
		}
	}

	imports := p.import_stmts()

	mut stmts := []ast.Stmt
	for p.tok != .eof {
		p.parse_attributes()
		stmts.push(p.toplevel_stmt())
		if p.should_abort {
			break
		}
	}

	return ast.File{
		path = path
		pkg_decl = pkg_decl
		imports = imports
		stmts = stmts
		warnings = p.warnings
		errors = p.errors
	}
}

fun (mut p Parser) package_decl() ast.PackageDecl {
	// Implicit main package
	if p.tok != .key_package {
		p.pkg_name = "main"
		return ast.PackageDecl{
			name = "main"
			full_name = "main"
		}
	}

	p.next()
	name := p.check_name()
	mut full_name := name
	lib_part := os.PATH_SEP + 'lib' + os.PATH_SEP
	if full_name != 'main' and p.path.contains(lib_part) {
		rel_path := p.path.substr(p.path.index(lib_part) + lib_part.length, p.path.length)
		full_name = rel_path.substr(0, rel_path.last_index(os.PATH_SEP)).replace(os.PATH_SEP, '.')
		if not full_name.ends_with(name) {
			return ast.PackageDecl{}
		}
	}
	p.pkg_name = full_name
	return ast.PackageDecl{
		name = name
		full_name = full_name
	}
}

// Parses one or more attributes
fun (mut p Parser) parse_attributes() {
	for p.tok == .attr {
		pos := p.tok.pos
		name := p.val
		p.next()
		mut value := ''
		if p.tok == .colon {
			p.next()
			value = p.val
			p.check(.string)
		}
		p.attributes.push(ast.Attribute{
			name = name
			value = value
			pos = pos
		})
	}
}

fun (mut p Parser) parse_block() []ast.Stmt {
	p.check(.lcur)
	mut bstmts := []ast.Stmt
	for not [token.TokenKind.eof, .rcur].contains(p.tok) {
		bstmts.push(p.stmt())
		if p.should_abort {
			break
		}
	}
	p.check(.rcur)
	return bstmts
}

fun (mut p Parser) check (expected token.TokenKind) {
	if p.tok != expected {
		p.error('unexpected ${p.tok}, expecting ${expected}')
	}
	p.next()
}

fun (mut p Parser) check_name() string {
	val := p.val
	p.check(.name)
	return val
}

fun (mut p Parser) check_pub() bool {
	if p.tok == .key_pub {
		p.next()
		return true
	}
	return false
}

fun (mut p Parser) parse_lang() ast.Language {
	if p.tok != .hash {
		return .bait
	}
	lang := ast.prefix_to_lang(p.val)
	p.next()
	p.check(.dot)
	return lang
}

fun (p Parser) prepend_pkg(val string) string {
	if p.pkg_name == 'builtin' or p.pkg_name == 'main' or p.pkg_name.length == 0{
		return val
	}
	return p.pkg_name + '.' + val
}

fun (mut p Parser) prepend_expr_pkg(val string) string {
	if p.expr_pkg.length == 0 or p.expr_pkg == p.pkg_name {
		return val
	}
	res := p.expr_pkg + '.' + val
	p.expr_pkg = ''
	return res
}

fun (mut p Parser) next() {
	if p.next_tok == .unknown {
		p.tok = p.lexer.next()
	} else {
		p.tok = p.next_tok
		p.next_tok = .unknown
	}
	p.val = p.lexer.val
}

fun (mut p Parser) peek() token.TokenKind {
	if p.next_tok == .unknown {
		p.next_tok = p.lexer.next()
	}
	return p.next_tok
}

fun (p Parser) warn(msg string) {
	p.warnings.push(errors.Message{
		kind = .warning
		path = p.path
		pos = p.tok.pos
		title = 'warning'
		msg = msg
	})
}

fun (mut p Parser) error(msg string){
	p.errors.push(errors.Message{
		kind = .error
		path = p.path
		pos = p.tok.pos
		title = 'error'
		msg = msg
	})
	p.should_abort = true
}
