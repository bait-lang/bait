// Copyright (c) 2023-present Lukas Neubert and contributors (see AUTHORS.md).
// This Source Code is subject to the terms of the Mozilla Public License 2.0.
package parser

import bait.ast
import bait.token

fun (p Parser) toplevel_stmt() ast.Stmt {
	match p.tok.kind {
		.key_pub{
			return p.pub_stmt()
		}
		.hash {
			return p.expr_stmt()
		}
		.key_const{
			return p.const_decl()
		}
		.key_enum{
			return p.enum_decl()
		}
		.key_fun{
			return p.fun_decl()
		}
		.key_global{
			return p.global_decl()
		}
		.key_interface{
			return p.interface_decl()
		}
		.key_struct{
			return p.struct_decl()
		}
		.key_type{
			return p.type_decl()
		}
		else{
			if p.pkg_name == 'main' {
				return p.script_mode_main()
			}
			p.error('bad toplevel token: kind = ${p.tok.kind}, val = ${p.tok.val}')
		}
	}
}

fun (p Parser) pub_stmt() ast.Stmt {
	match p.next_tok.kind {
		.key_const{
			return p.const_decl()
		}
		.key_enum{
			return p.enum_decl()
		}
		.key_fun{
			return p.fun_decl()
		}
		.key_interface{
			return p.interface_decl()
		}
		.key_struct{
			return p.struct_decl()
		}
		.key_type{
			return p.type_decl()
		}
		else{
			p.error('cannot use pub keyword before ${p.next_tok.kind}')
		}
	}
}

fun (p Parser) script_mode_main() ast.FunDecl {
	if not p.pref.is_script {
		p.warn('declare the main function or use the --script option')
	}
	mut stmts := []ast.Stmt
	for  not p.should_abort and p.tok.kind != .eof {
		stmts.push(p.stmt())
	}
	mut node := ast.FunDecl{
		name = 'main'
	}
	p.table.fun_decls['main'] = node
	node.stmts = stmts
	return node
}

fun (p Parser) stmt() ast.Stmt {
	match p.tok.kind {
		.key_assert {
			return p.assert_stmt()
		}
		.key_mut, .name {
			return p.assign_or_expr_stmt()
		}
		.key_break, .key_continue {
			return p.control_stmt()
		}
		.key_for {
			return p.for_loop()
		}
		.key_return {
			return p.return_stmt()
		}
		else{
			return p.expr_stmt()
		}
	}
}

fun (p Parser) assign_or_expr_stmt()ast.Stmt{
	left := p.expr(0)
	if p.tok.kind.is_assign() {
		return p.assign_stmt(left)
	}
	return ast.ExprStmt{expr = left}
}

fun (p Parser) assert_stmt() ast.AssertStmt{
	p.next()
	pos := p.tok.pos
	expr := p.expr(0)
	return ast.AssertStmt{
		expr = expr
		pos = pos
	}
}

fun (p Parser) assign_stmt(left ast.Expr) ast.AssignStmt {
	pos := p.tok.pos
	op := p.tok.kind
	p.next()
	right := p.expr(0)
	return ast.AssignStmt{
		op = op
		left = left
		right = right
		pos = pos
	}
}

fun (p Parser) const_decl()ast.ConstDecl{
	pos := p.tok.pos
	is_pub := p.check_pub()
	p.next()
	name := p.prepend_pkg(p.check_name())
	p.check(.decl_assign)
	expr := p.expr(0)
	typ := p.infer_expr_type(expr)
	p.table.global_scope.register(name, ast.ScopeObject{
		typ = typ
		kind = .constant
		is_pub = is_pub
		expr = expr
	})
	return ast.ConstDecl{
		name = name
		expr = expr
		pos = pos
	}
}

fun (p Parser) control_stmt()ast.LoopControlStmt{
	pos := p.tok.pos
	p.next()
	return ast.LoopControlStmt{
		kind = p.prev_tok.kind
		pos = pos
	}
}

fun (p Parser) expr_stmt()ast.ExprStmt{
	expr := p.expr(0)
	return ast.ExprStmt{
		expr = expr
	}
}

fun (p Parser) enum_decl()ast.EnumDecl{
	pos := p.tok.pos
	is_pub := p.check_pub()
	p.next()
	name := p.prepend_pkg(p.check_name())
	p.check(.lcur)
	mut variants := []string
	mut fields := []ast.EnumField
	for p.tok.kind != .rcur {
		fname := p.check_name()
		mut expr := ast.EmptyExpr{} as ast.Expr
		if p.tok.kind == .decl_assign {
			p.next()
			expr = p.expr(0)
		}
		variants.push(fname)
		fields.push(ast.EnumField{
			name = fname
			expr = expr
		})
	}
	p.check(.rcur)
	p.table.register_sym(ast.TypeSymbol{
		name = name
		is_pub = is_pub
		pkg = p.pkg_name
		kind = .enum_
		info = ast.EnumInfo{
			vals = variants
		}
	})
	return ast.EnumDecl{
		name = name
		fields = fields
		pos = pos
	}
}

fun (p Parser) for_loop()ast.Stmt{
	pos := p.tok.pos
	p.next()
	if p.next_tok.kind == .decl_assign {
		p.is_for_init = true
		init := p.assign_or_expr_stmt()
		p.is_for_init = false
		p.check(.semicolon)
		cond := p.expr(0)
		p.check(.semicolon)
		inc := p.stmt()
		stmts := p.parse_block()
		return ast.ForClassicLoop{
			init = init
			cond = cond
			inc = inc
			stmts = stmts
			pos = pos
		}
	}
	if p.next_tok.kind == .key_in or p.next_tok.kind == .comma {
		mut idxvar := ''
		if p.next_tok.kind == .comma {
			idxvar = p.check_name()
			p.next()
		}
		valvar := p.check_name()
		p.check(.key_in)
		p.is_struct_possible = false
		expr := p.expr(0)
		p.is_struct_possible = true
		stmts := p.parse_block()
		return ast.ForInLoop{
			idxvar = idxvar
			valvar = valvar
			expr = expr
			stmts = stmts
			pos = pos
		}
	}
	cond := p.expr(0)
	stmts := p.parse_block()
	return ast.ForLoop{
		cond = cond
		stmts = stmts
		pos = pos
	}
}

fun (p Parser) fun_decl()ast.FunDecl{
	pos := p.tok.pos
	is_pub := p.check_pub()
	p.check(.key_fun)

	mut lang := ast.Language.bait
	mut is_method := false
	mut params := []ast.Param
	if p.tok.kind == .lpar {
		is_method = true
		p.next()
		rec_name := p.check_name()
		rec_type := p.parse_type()
		params.push(ast.Param{
			name = rec_name
			typ = rec_type
		})
		p.check(.rpar)
	}

	mut name := ''
	if p.tok.kind == .hash {
		// language
		lang = .js
		name += p.tok.val + '__'
		p.next()
		p.check(.dot)

		// foreign package
		name += p.check_name() + '.'
		p.check(.dot)
	}

	// normal function name
	name += p.check_name()
	if not is_method and lang == .bait {
		name = p.prepend_pkg(name)
	}

	p.check(.lpar)
	params = params.concat(p.fun_params())
	p.check(.rpar)

	mut return_type := ast.VOID_TYPE
	if p.tok.kind != .lcur and pos.line == p.tok.pos.line {
		return_type = p.parse_type()
	}

	mut node := ast.FunDecl{
		is_test = name.starts_with('test_')
		is_pub = is_pub
		name = name
		params = params
		return_type = return_type
		attrs = p.attributes
		lang = lang
		pos = pos
	}
	p.attributes = []ast.Attribute

	if is_method {
		sym := p.table.get_sym(params[0].typ)
		// TODO move this error into checker
		if sym.has_method(name) {
			p.error('Method "${name}" already exists on type "${sym.name}"')
		}
		sym.methods.push(node)
	} else {
		if p.table.fun_decls.contains(name) {
			p.table.redefined_funs.push(name)
		}
		p.table.fun_decls[name] = node
		mut param_types := []ast.Type
		for param in params {
			param_types.push(param.typ)
		}
		typ := p.table.find_or_register_fun(param_types, return_type)
	}

	if lang == .bait {
		node.stmts = p.parse_block()
	}

	node.is_method = is_method
	return node
}

fun (p Parser) fun_params() []ast.Param{
	mut params := []ast.Param
	for p.tok.kind != .rpar{
		pos := p.tok.pos
		param := ast.Param{
			name = p.check_name()
			typ = p.parse_type()
			pos = pos
		}
		params.push(param)
		if p.tok.kind != .rpar{
			p.check(.comma)
		}
	}
	return params
}

fun (p Parser) global_decl()ast.GlobalDecl{
	pos := p.tok.pos
	p.check(.key_global)
	name := p.prepend_pkg(p.check_name())
	p.check(.decl_assign)
	expr := p.expr(0)
	typ := p.infer_expr_type(expr)
	p.table.global_scope.register(name, ast.ScopeObject{
		typ = typ
		kind = .global_
	})
	return ast.GlobalDecl{
		name = name
		expr = expr
		pos = pos
	}
}

fun (p Parser) interface_decl()ast.InterfaceDecl{
	pos := p.tok.pos
	is_pub := p.check_pub()
	p.check(.key_interface)
	lang := p.parse_lang()
	name := p.prepend_lang(lang, p.check_name())

	// TODO investigate why registration of the sym after loop with methods as struct field does not work
	tsym := ast.TypeSymbol{
		kind = .interface_
		name = name
		is_pub = is_pub
		pkg = p.pkg_name
	}
	typ := p.table.register_sym(tsym)

	mut methods := []ast.FunDecl
	mut fields := []ast.StructField
	p.check(.lcur)
	for p.tok.kind != .rcur {
		if p.tok.kind == .key_fun {
			p.next()
			fun_name := p.check_name()
			p.check(.lpar)
			// The first param is a fictional receiver in this case
			mut params := [ast.Param{
				name = '_'
				typ = typ
			}]
			params = params.concat(p.fun_params())
			p.check(.rpar)
			mut return_type := ast.VOID_TYPE
			if p.tok.pos.line == p.prev_tok.pos.line {
				return_type = p.parse_type()
			}
			m := ast.FunDecl{
				lang = .js
				name = fun_name
				params = params
				return_type = return_type
				is_pub = true
				is_method = true
			}
			methods.push(m)
			tsym.methods.push(m)
			continue
		}

		if p.tok.kind == .name {
			fields.push(ast.StructField{
				name = p.check_name()
				typ = p.parse_type()
			})
			continue
		}

		// TODO error
	}
	p.next()

	tsym.info = ast.StructInfo {
		fields = fields
	}

	return ast.InterfaceDecl{
		lang = lang
		name = name
		typ = typ
		methods = methods
		fields = fields
		pos = pos
	}
}

fun (p Parser) return_stmt()ast.ReturnStmt{
	pos := p.tok.pos
	p.check(.key_return)
	mut expr := ast.EmptyExpr{} as ast.Expr
	if p.tok.pos.line == pos.line {
		expr = p.expr(0)
	}
	return ast.ReturnStmt{
		expr = expr
		pos = pos
	}
}

fun (p Parser) type_decl() ast.TypeDecl {
	pos := p.tok.pos
	is_pub := p.check_pub()
	p.next()
	name := p.prepend_pkg(p.check_name())
	p.check(.decl_assign)
	is_fun_type := p.tok.kind == .key_fun
	mut variants := []ast.Type
	variants.push(p.parse_type())

	// Function type
	if is_fun_type {
		sym := p.table.get_sym(variants[0])
		typ := p.table.register_sym(ast.TypeSymbol{
			kind = .fun_
			name = name
			is_pub = is_pub
			pkg = p.pkg_name
			info = sym.info
		})
		return ast.TypeDecl{
			name = name
			typ = typ
			pos = pos
		}
	}

	// Alias type
	if p.tok.kind != .pipe {
		typ := p.table.register_sym(ast.TypeSymbol{
			kind = .alias_type
			name = name
			parent = variants[0]
			is_pub = is_pub
			pkg = p.pkg_name
		})
		return ast.TypeDecl{
			name = name
			typ = typ
			pos = pos
		}
	}

	// Sum type
	for p.tok.kind == .pipe {
		p.next()
		variants.push(p.parse_type())
	}
	typ := p.table.register_sym(ast.TypeSymbol{
		kind = .sum_type
		name = name
		is_pub = is_pub
		pkg = p.pkg_name
		info = ast.SumTypeInfo{
			variants = variants
		}
	})
	return ast.TypeDecl{
		name = name
		typ = typ
		pos = pos
	}
}
