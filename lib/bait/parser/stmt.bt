// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0
package parser

import bait.ast
import bait.token

fun (mut p Parser) toplevel_stmt() ast.Stmt {
	match p.tok {
		.key_pub{
			return p.pub_stmt()
		}
		.hash {
			return p.expr_stmt()
		}
		.key_const{
			return p.const_decl()
		}
		.key_enum{
			return p.enum_decl()
		}
		.key_fun{
			return p.fun_decl()
		}
		.key_global{
			return p.global_decl()
		}
		.key_interface{
			return p.interface_decl()
		}
		.key_struct{
			return p.struct_decl()
		}
		.key_type{
			return p.type_decl()
		}
		else{
			if p.pkg_name == 'main' {
				return p.script_mode_main()
			}
			p.error('bad toplevel token: kind = ${p.tok}, val = ${p.val}')
			return ast.EmptyStmt{}
		}
	}
}

fun (mut p Parser) pub_stmt() ast.Stmt {
	match p.peek() {
		.key_const{
			return p.const_decl()
		}
		.key_enum{
			return p.enum_decl()
		}
		.key_fun{
			return p.fun_decl()
		}
		.key_interface{
			return p.interface_decl()
		}
		.key_struct{
			return p.struct_decl()
		}
		else{
			p.error('cannot use pub keyword before ${p.peek()}')
			return ast.EmptyStmt{}
		}
	}
}

fun (mut p Parser) script_mode_main() ast.FunDecl {
	if not p.pref.is_script {
		p.warn('declare the main function or use the --script option')
	}
	mut stmts := []ast.Stmt
	for not p.should_abort and p.tok != .eof {
		stmts.push(p.stmt())
	}
	mut node := ast.FunDecl{
		name = 'main'
		return_type = ast.VOID_TYPE
	}
	p.table.fun_decls['main'] = node
	node.stmts = stmts
	return node
}

fun (mut p Parser) stmt() ast.Stmt {
	match p.tok {
		.name {
			return p.stmt_with_name()
		}
		.key_assert {
			return p.assert_stmt()
		}
		.key_mut {
			return p.assign_stmt()
		}
		.key_break, .key_continue {
			return p.loop_control_stmt()
		}
		.key_for {
			return p.for_loop('')
		}
		.key_if {
			return p.if_expr()
		}
		.key_match {
			return p.match_expr()
		}
		.key_return {
			return p.return_stmt()
		}
		else{
			return p.expr_stmt()
		}
	}
}

fun (mut p Parser) stmt_with_name()ast.Stmt{
	if p.peek() == .colon {
		label := p.val
		p.next()
		p.next()
		return p.for_loop(label)
	}

	left := p.expr(.lowest)
	if p.tok.is_assign() {
		return p.partial_assign_stmt(left)
	}
	return ast.ExprStmt{expr = left}
}

fun (mut p Parser) assert_stmt() ast.AssertStmt{
	p.next()
	pos := p.pos()
	expr := p.expr(.lowest)
	return ast.AssertStmt{
		expr = expr
		pos = pos
	}
}

fun (mut p Parser) assign_stmt() ast.AssignStmt {
	left := p.expr(.lowest)
	return p.partial_assign_stmt(left)
}

fun (mut p Parser) partial_assign_stmt(left ast.Expr) ast.AssignStmt {
	op := p.tok
	p.next()
	right := p.expr(.lowest)
	return ast.AssignStmt{
		op = op
		left = left
		right = right
		pos = (left as ast.EmptyExpr).pos
	}
}

fun (mut p Parser) const_decl()ast.ConstDecl{
	pos := p.pos()
	is_pub := p.check_pub()
	p.next()

	lang := p.parse_lang()
	mut name := ''

	// foreign package
	if lang == .js and p.peek() == .dot {
		name = p.check_name() + '.'
		p.next()
	}

	// normal name and bait package
	name += p.check_name()
	if lang == .bait {
		name = p.prepend_pkg(name)
	}

	p.check(.decl_assign)
	mut expr := ast.EmptyExpr{} as ast.Expr
	mut typ := ast.PLACEHOLDER_TYPE
	if lang == .bait {
		expr = p.expr(.lowest)
		typ = p.infer_expr_type(expr)
	} else {
		typ = p.parse_type()
	}

	p.table.global_scope.register(lang.prepend_to(name), ast.ScopeObject{
		typ = typ
		kind = .constant
		is_pub = is_pub
		pkg = p.pkg_name
		expr = expr
	})

	return ast.ConstDecl{
		name = name
		expr = expr
		typ = typ
		pos = pos
		lang = lang
	}
}

fun (mut p Parser) loop_control_stmt()ast.LoopControlStmt{
	pos := p.pos()
	kind := p.tok
	p.next()

	mut label := ''
	if p.tok == .name and p.pos().line == pos.line {
		label = p.check_name()
	}

	return ast.LoopControlStmt{
		kind = kind
		label = label
		pos = pos
	}
}

fun (mut p Parser) expr_stmt()ast.ExprStmt{
	expr := p.expr(.lowest)
	return ast.ExprStmt{
		expr = expr
	}
}

fun (mut p Parser) enum_decl()ast.EnumDecl{
	pos := p.pos()
	is_pub := p.check_pub()
	p.next()

	lang := p.parse_lang()
	mut name := p.check_name()
	if lang == .bait {
		name = p.prepend_pkg(name)
	} else {
		name = lang.prepend_to(name)
	}

	p.check(.lcur)
	mut variants := []string
	mut fields := []ast.EnumField
	for p.tok != .rcur {
		fpos := p.pos()
		fname := p.check_name()
		mut expr := ast.EmptyExpr{} as ast.Expr
		if p.tok == .decl_assign {
			p.next()
			expr = p.expr(.lowest)
		}
		variants.push(fname)
		fields.push(ast.EnumField{
			name = fname
			expr = expr
			pos = fpos
		})
	}
	p.check(.rcur)

	p.table.register_sym(ast.TypeSymbol{
		name = name
		is_pub = is_pub
		pkg = p.pkg_name
		kind = .enum_
		info = ast.EnumInfo{
			vals = variants
		}
	})

	return ast.EnumDecl{
		lang = lang
		name = name
		fields = fields
		pos = pos
	}
}

fun (mut p Parser) global_decl()ast.GlobalDecl{
	pos := p.pos()
	p.check(.key_global)
	name := p.prepend_pkg(p.check_name())
	p.check(.decl_assign)
	expr := p.expr(.lowest)
	typ := p.infer_expr_type(expr)
	p.table.global_scope.register(name, ast.ScopeObject{
		is_mut = true
		typ = typ
		kind = .global_
	})
	return ast.GlobalDecl{
		name = name
		expr = expr
		pos = pos
	}
}

fun (mut p Parser) interface_decl()ast.InterfaceDecl{
	pos := p.pos()
	is_pub := p.check_pub()
	p.check(.key_interface)
	lang := p.parse_lang()
	mut name := lang.prepend_to(p.check_name())
	if lang != .bait and p.tok == .dot {
		name += '.'
		p.next()
		name += p.check_name()
	}

	// TODO investigate why registration of the sym after loop with methods as struct field does not work
	mut tsym := ast.TypeSymbol{
		kind = .interface_
		name = name
		is_pub = is_pub
		pkg = p.pkg_name
	}
	typ := p.table.register_sym(tsym)

	mut mut_idx := -1
	mut pub_idx := -1
	mut pub_mut_idx := -1
	mut global_idx := -1
	mut field_is_mut := false
	mut field_is_pub := false
	mut field_is_global := false

	mut methods := []ast.FunDecl
	mut fields := []ast.StructField

	p.check(.lcur)
	for p.tok != .rcur {
		if p.tok == .key_mut {
			if mut_idx != -1 {
				p.error('redefinition of "mut" section')
				return ast.InterfaceDecl{}
			}
			p.next()
			p.check(.colon)
			mut_idx = fields.length
			field_is_mut = true
			field_is_pub = false
			field_is_global = false
		} else if p.tok == .key_pub {
			p.next()
			if p.tok == .key_mut {
				if pub_mut_idx != -1 {
					p.error('redefinition of "pub mut" section')
					return ast.InterfaceDecl{}
				}
				p.next()
				pub_mut_idx = fields.length
				field_is_mut = true
				field_is_pub = true
				field_is_global = false
			} else {
				if pub_idx != -1 {
					p.error('redefinition of "pub" section')
					return ast.InterfaceDecl{}
				}
				pub_idx = fields.length
				field_is_mut = false
				field_is_pub = true
				field_is_global = false
			}
			p.check(.colon)
		} else if p.tok == .key_global {
			if global_idx != -1 {
				p.error('redefinition of "global" section')
				return ast.InterfaceDecl{}
			}
			p.next()
			p.check(.colon)
			global_idx = fields.length
			field_is_mut = true
			field_is_pub = true
			field_is_global = true
		}

		fname := p.check_name()
		if p.tok == .lpar {
			m := p.interface_method(fname, typ)
			methods.push(m)
			tsym.methods.push(m)
			continue
		}

		fields.push(ast.StructField{
			name = fname
			typ = p.parse_type()
			is_mut = field_is_mut
			is_pub = field_is_pub
			is_global = field_is_global
		})
	}
	p.next()

	tsym.info = ast.StructInfo {
		fields = fields
	}

	return ast.InterfaceDecl{
		lang = lang
		name = name
		typ = typ
		methods = methods
		fields = fields
		pos = pos
	}
}

fun (mut p Parser) interface_method(name string, rec_type ast.Type) ast.FunDecl {
	p.check(.lpar)
	// The first param is a fictional receiver in this case
	mut params := [ast.Param{
		name = '_'
		typ = rec_type
	}]
	params = params.concat(p.fun_params())
	par_line := p.pos().line
	p.check(.rpar)
	mut return_type := ast.VOID_TYPE
	if p.pos().line == par_line {
		return_type = p.parse_type()
	}
	return ast.FunDecl{
		lang = .js
		name = name
		params = params
		return_type = return_type
		is_pub = true
		is_method = true
	}
}

fun (mut p Parser) return_stmt()ast.ReturnStmt{
	pos := p.pos()
	p.check(.key_return)
	mut expr := ast.EmptyExpr{} as ast.Expr
	if p.tok != .semicolon {
		expr = p.expr(.lowest)
	}
	p.skip(.semicolon)
	return ast.ReturnStmt{
		expr = expr
		pos = pos
	}
}

fun (mut p Parser) type_decl() ast.TypeDecl {
	pos := p.pos()
	is_pub := p.check_pub()
	p.next()

	lang := p.parse_lang()
	mut name := p.check_name()
	if lang == .bait {
		name = p.prepend_pkg(name)
	} else {
		name = lang.prepend_to(name)
	}

	p.check(.decl_assign)
	is_fun_type := p.tok == .key_fun
	mut variants := []ast.Type
	variants.push(p.parse_type())

	// Function type
	if is_fun_type {
		sym := p.table.get_sym(variants[0])
		typ := p.table.register_sym(ast.TypeSymbol{
			kind = .fun_
			name = name
			is_pub = is_pub
			pkg = p.pkg_name
			info = sym.info
		})
		return ast.TypeDecl{
			lang = lang
			name = name
			typ = typ
			pos = pos
		}
	}

	// Alias type
	if p.tok != .pipe {
		typ := p.table.register_sym(ast.TypeSymbol{
			kind = .alias_type
			name = name
			parent = variants[0]
			is_pub = is_pub
			pkg = p.pkg_name
		})
		return ast.TypeDecl{
			lang = lang
			name = name
			typ = typ
			pos = pos
		}
	}

	// Sum type
	for p.tok == .pipe {
		p.next()
		variants.push(p.parse_type())
	}
	typ := p.table.register_sym(ast.TypeSymbol{
		kind = .sum_type
		name = name
		is_pub = is_pub
		pkg = p.pkg_name
		info = ast.SumTypeInfo{
			variants = variants
		}
	})
	return ast.TypeDecl{
		lang = lang
		name = name
		typ = typ
		pos = pos
	}
}
