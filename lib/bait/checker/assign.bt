// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package checker

import bait.ast
import bait.context
import bait.token

fun (mut c Checker) decl_assign(mut node ast.AssignStmt) {
	if node.left is ast.BlankIdent {
		// Note: Side types must be set for BlankIdent due to backend limitations
		_ = c.decl_right_side(mut node)
		return
	}

	if not (node.left is ast.Ident) {
		c.error('cannot declare a variable with a non-identifier', node.pos)
		return
	}

	left := node.left as ast.Ident

	// Name shadowing
	c.scope.expect_unknown(left.name) or {
		c.error(err, node.pos)
		return
	}

	// Name conventions
	if left.name.length > 1 and left.name[0] == `_` {
		c.error("variable `${left.name}` cannot start with `_`", node.pos)
		return
	}

	typ := c.decl_right_side(mut node)

	// Register identifier
	c.scope.register(left.name, context.ScopeObject{
		kind = .variable
		typ = typ
		is_mut = left.is_mut
	})
	_ = c.expr(node.left)
}

fun (mut c Checker) decl_right_side(mut node ast.AssignStmt) ast.Type {
	typ := c.non_void_expr(node.right) or {
		return ast.ERROR_TYPE
	}

	node.left_type = typ
	node.right_type = typ

	return typ
}

fun (mut c Checker) assign_stmt(mut node ast.AssignStmt) {
	if node.op == .decl_assign {
		c.decl_assign(mut node)
		return
	}

	c.is_lhs_assign = true
	node.left_type = c.expr(node.left)
	c.is_lhs_assign = false

	c.expected_type = node.left_type
	node.right_type = c.expr(node.right)

	if node.left_type == ast.ERROR_TYPE or node.right_type == ast.ERROR_TYPE {
		return
	}

	if node.left is ast.BlankIdent {
		return
	}

	// Mutability check
	if node.left is ast.Ident {
		left := node.left as ast.Ident
		if not left.is_mut {
			c.error('cannot assign to immutable variable "${left.name}"', left.pos)
			return
		}
	} else if node.left is ast.SelectorExpr {
		mut left := node.left as ast.SelectorExpr
		if not c.is_field_mutable(left) {
			return
		}

		for left.expr is ast.SelectorExpr {
			left = left.expr as ast.SelectorExpr
			if not c.is_field_mutable(left) {
				return
			}
		}

		if left.expr is ast.Ident {
			lident := left.expr as ast.Ident
			if not lident.is_mut {
				c.error('cannot assign to field of immutable variable `${lident.name}`', node.pos)
				return
			}
		}
	}

	// Type check
	if not c.check_types(node.right_type, node.left_type) {
		c.error('cannot assign type ${c.table.type_name(node.right_type)} to ${c.table.type_name(node.left_type)}', node.pos)
	}
}

fun (c Checker) is_field_mutable(left ast.SelectorExpr) bool {
	sym := c.table.get_sym(left.expr_type)
	field := sym.find_field(left.field_name, c.table)
	if (not field.is_mut or sym.pkg != c.pkg) and not field.is_global {
		c.error('field `${sym.mix_name}.${left.field_name}` is immutable', left.pos)
		return false
	}
	return true
}
