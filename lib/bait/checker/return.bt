// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package checker

import bait.ast

fun (mut c Checker) return_stmt(mut node ast.ReturnStmt) {
	c.expected_type = c.table.unwrap_result(c.cur_fun.return_type)
	expr_type := c.expr(node.expr)
	if expr_type == ast.ERROR_TYPE {
		return
	}

	if not c.check_types(expr_type, c.cur_fun.return_type) {
		mut msg := ''
		if c.cur_fun.return_type == ast.VOID_TYPE {
			msg = 'function `${c.cur_fun.name}` should return nothing'
		} else {
			msg = 'expected return value of type ${c.table.type_name(c.cur_fun.return_type)}'
		}
		if expr_type > ast.VOID_TYPE {
			msg += ', got ${c.table.type_name(expr_type)}'
		}
		c.error(msg, node.pos)
	}

	return_sym := c.table.get_sym(c.cur_fun.return_type)
	if return_sym.kind == .result {
		expr_sym := c.table.get_sym(expr_type)
		if expr_sym.kind == .result {
			return
		}

		mut res_expr := ast.StructInit{
			name = 'Result'
		}

		if expr_type != ast.VOID_TYPE {
			res_expr.fields = [ast.StructInitField{
				name = 'data'
				expr = node.expr
			}]
		}

		node.expr = res_expr
	}
}

// Check all statements in a block where the last statement should return a value
fun (mut c Checker) stmts_with_return(stmts []ast.Stmt) ast.Type {
	for i := 0; i < stmts.length - 1; i += 1 {
		c.stmt(mut stmts[i])
	}

	if stmts.length == 0 {
		return ast.VOID_TYPE
	}

	was_expecting_expr := c.expecting_expr
	c.expecting_expr = true
	mut last := stmts[stmts.length - 1]
	c.stmt(mut last)
	c.expecting_expr = was_expecting_expr

	return match last {
		ast.ExprStmt { last.typ }
		ast.IfMatch { last.typ }
		else { ast.VOID_TYPE }
	}
}

fun (c Checker) stmt_returns (stmt ast.Stmt) bool {
	if stmt is ast.ExprStmt {
		return stmt.typ != ast.VOID_TYPE
	}

	if stmt is ast.IfMatch {
		return c.returns
	}

	return false
}

fun has_toplevel_return(stmts []ast.Stmt) bool {
	for stmt in stmts {
		if stmt is ast.ReturnStmt {
			return true
		}

		if is_noreturn(stmt) {
			return true
		}
	}

	return false
}

fun is_noreturn(stmt ast.Stmt) bool {
	if stmt is ast.ExprStmt {
		expr := stmt.expr
		return match expr {
			ast.CallExpr { expr.noreturn }
			else { false }
		}
	}

	if stmt is ast.LoopControlStmt {
		return true
	}

	return false
}
