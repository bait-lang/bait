// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0
package checker

import bait.ast
import bait.token

fun (mut c Checker) expr(expr ast.Expr) ast.Type {
	if expr is ast.AnonFun {
		return c.anon_fun(expr)
	} else if expr is ast.ArrayInit {
		return c.array_init(expr)
	} else if expr is ast.AsCast {
		return c.as_cast(expr)
	} else if expr is ast.BoolLiteral {
		return ast.BOOL_TYPE
	} else if expr is ast.CallExpr {
		return c.call_expr(expr)
	} else if expr is ast.CharLiteral {
		return ast.U8_TYPE
	} else if expr is ast.CompTimeVar {
		return c.comp_time_var(expr)
	} else if expr is ast.EnumVal {
		return c.enum_val(expr)
	} else if expr is ast.FloatLiteral {
		return ast.F64_TYPE
	} else if expr is ast.HashExpr {
		return c.hash_expr(expr)
	} else if expr is ast.Ident {
		return c.ident(expr)
	} else if expr is ast.IfExpr {
		return c.if_expr(expr, true)
	} else if expr is ast.IndexExpr {
		return c.index_expr(expr)
	} else if expr is ast.InfixExpr {
		return c.infix_expr(expr)
	} else if expr is ast.IntegerLiteral {
		return ast.I32_TYPE
	} else if expr is ast.MapInit {
		return c.map_init(expr)
	} else if expr is ast.MatchExpr {
		return c.match_expr(expr, true)
	} else if expr is ast.ParExpr {
		return c.par_expr(expr)
	} else if expr is ast.PrefixExpr {
		return c.prefix_expr(expr)
	} else if expr is ast.SelectorExpr {
		return c.selector_expr(expr)
	} else if expr is ast.StringLiteral {
		return c.string_literal(expr)
	} else if expr is ast.StringInterLiteral {
		return c.string_inter_literal(expr)
	} else if expr is ast.StructInit {
		return c.struct_init(expr)
	} else if expr is ast.TypeOf {
		return c.type_of(expr)
	} else {
		e := expr as ast.EmptyExpr
		c.error('unexpected expr: ${e}', e.pos)
	}
}

fun (mut c Checker) anon_fun(node ast.AnonFun) ast.Type {
	root_fun_decl := c.cur_fun
	c.cur_fun = node.decl
	c.open_scope()
	c.fun_params(node.decl.params)
	c.stmts(node.decl.stmts)
	c.close_scope()
	c.cur_fun = root_fun_decl
	return node.typ
}

fun (mut c Checker) array_init(mut node ast.ArrayInit) ast.Type {
	// Type init
	if node.exprs.length == 0 {
		// Typeless array init
		if node.elem_type == ast.PLACEHOLDER_TYPE {
			node.typ = c.expected_type
			return node.typ
		}

		// Validate fields if present
		if not (node.length_expr is ast.EmptyExpr) {
			typ := c.expr(node.length_expr)
			if not c.check_types(typ, ast.I32_TYPE) {
				c.error('expected i32, got ${c.table.type_name(typ)}', node.pos)
			}
		}
		if not (node.cap_expr is ast.EmptyExpr) {
			typ := c.expr(node.cap_expr)
			if not c.check_types(typ, ast.I32_TYPE) {
				c.error('expected i32, got ${c.table.type_name(typ)}', node.pos)
			}
		}

		return node.typ
	}

	// Expressions init
	for i, e in node.exprs {
		typ := c.expr(e)
		if i == 0 {
			node.elem_type = typ
			c.expected_type = typ
		}
		if not c.check_types(typ, node.elem_type) {
			c.error('expected element type ${c.table.type_name(node.elem_type)}, got ${c.table.type_name(typ)}', node.pos)
		}
	}
	node.typ = c.table.find_or_register_array(node.elem_type)
	return node.typ
}

fun (mut c Checker) as_cast(mut node ast.AsCast) ast.Type {
	expr_type := c.expr(node.expr)
	node.expr_type = expr_type
	expr_sym := c.table.get_sym(expr_type)

	// Check the target exists
	target_sym := c.table.get_sym(node.target)
	if not c.does_type_exist(target_sym, node.pos) {
		return expr_type
	}

	// TODO check cast is possible (e.g. prevent string to i32, ...)

	// Pointer casts
	// TODO test
	if expr_type.get_nr_amp() >= 1 and node.target.get_nr_amp() == 0 {
		c.error('cannot cast to normal type from pointer', node.pos)
		return expr_type
	}

	// Smartcast sumtypes
	// TODO check if expr is part of target
	if expr_sym.kind == .sum_type and node.expr is ast.Ident {
		expr := node.expr as ast.Ident
		c.scope.update_type(expr.name, node.target)
	}

	return node.target
}

const SUPPORTED_COMPTIME_VARS := [
	'PKG',
	'FILE',
	'ABS_FILE',
	'LINE',
	'FILE_LINE',
	'FUN',
	'BAITEXE',
	'BAITDIR',
	'BAITHASH',
]

fun (mut c Checker) comp_time_var(node ast.CompTimeVar) ast.Type {
	if not SUPPORTED_COMPTIME_VARS.contains(node.name) {
		c.error('unsupported comptime var "${node.name}"', node.pos)
	}
	return ast.STRING_TYPE
}

fun (mut c Checker) enum_val(mut node ast.EnumVal) ast.Type {
	if node.name.length == 0 {
		node.typ = c.expected_type
	} else {
		node.typ = c.table.get_idx(node.name)
	}
	sym := c.table.get_sym(node.typ)
	if sym.kind == .placeholder {
		c.error('undefined enum ${node.name}', node.pos)
		return ast.PLACEHOLDER_TYPE
	}
	if sym.kind != .enum_ {
		c.error('expected type is not an enum, got ${sym.name}', node.pos)
		return ast.PLACEHOLDER_TYPE
	}
	if not sym.is_pub and sym.name.contains('.') and sym.pkg != c.pkg {
		c.error('enum ${sym.name} is private', node.pos)
		return ast.PLACEHOLDER_TYPE
	}
	info := sym.info as ast.EnumInfo
	if not info.vals.contains(node.val) {
		c.error('enum ${sym.name} has no value ${node.val}', node.pos)
		return ast.PLACEHOLDER_TYPE
	}
	node.name = sym.name
	return node.typ
}

fun (mut c Checker) hash_expr(node ast.HashExpr) ast.Type {
	if node.lang == .js and not c.is_js_file {
		c.warn('JS code can only be used in .js.bt files', node.pos)
	}
	return ast.VOID_TYPE
}

fun (mut c Checker) ident(mut node ast.Ident) ast.Type {
	mut obj := c.scope.get(node.name)

	if obj.kind == .constant and not obj.is_pub and obj.pkg != c.pkg {
		c.error('const ${node.name} is private', node.pos)
	}

	if obj.typ != ast.PLACEHOLDER_TYPE {
		node.is_mut = obj.is_mut
		return obj.typ
	}

	if not node.name.contains('.') and node.pkg != 'builtin' and node.pkg != 'main' {
		node.name = node.pkg + '.' + node.name
	}
	obj = c.table.global_scope.get(node.name)
	if obj.typ == ast.PLACEHOLDER_TYPE {
		c.error('undefined identifier ${node.name}', node.pos)
	}

	// unresolved constant
	if obj.typ == ast.VOID_TYPE {
		obj.typ = c.expr(obj.expr)
	}

	node.is_mut = obj.is_mut
	return obj.typ
}

fun (mut c Checker) index_expr(mut node ast.IndexExpr) ast.Type {
	node.left_type = c.expr(node.left)
	c.expr(node.index)
	sym := c.table.get_sym(node.left_type)
	if sym.kind == .array {
		info := sym.info as ast.ArrayInfo
		return info.elem_type
	}
	if sym.kind == .map {
		info := sym.info as ast.MapInfo
		return info.val_type
	}
	if sym.kind == .string {
		if c.is_lhs_assign{
			c.error('cannot assign to string index', node.pos)
			return ast.VOID_TYPE
		}
		return ast.U8_TYPE
	}
	return node.left_type
}

fun (mut c Checker) map_init(mut node ast.MapInit) ast.Type {
	// Empty init
	if node.keys.length == 0 {
		sym := c.table.get_sym(node.typ)
		info := sym.info as ast.MapInfo
		node.key_type = info.key_type
		node.val_type = info.val_type
		return node.typ
	}

	// Literal init
	for i, key in node.keys {
		key_type := c.expr(key)
		if i >= 1 {
			c.expected_type = node.val_type
		}
		val_type := c.expr(node.vals[i])

		if i == 0 {
			node.key_type = key_type
			node.val_type = val_type
		} else {
			if not c.check_types(key_type, node.key_type) {
				key_expr := key as ast.EmptyExpr
				c.error('expected key type ${c.table.type_name(node.key_type)}, got ${c.table.type_name(key_type)}', key_expr.pos)
			}
			if not c.check_types(val_type, node.val_type) {
				val_expr := node.vals[i] as ast.EmptyExpr
				c.error('expected value type ${c.table.type_name(node.val_type)}, got ${c.table.type_name(val_type)}', val_expr.pos)
			}
		}
	}
	node.typ = c.table.find_or_register_map(node.key_type, node.val_type)
	return node.typ
}

fun (mut c Checker) par_expr(node ast.ParExpr) ast.Type {
	return c.expr(node.expr)
}

fun (mut c Checker) prefix_expr(node ast.PrefixExpr) ast.Type {
	typ := c.expr(node.right)
	sym := c.table.get_sym(typ)

	// TODO disallow on unsigned numbers too
	if node.op == .minus and sym.kind != .number {
		c.error('cannot negate ${c.table.type_name(typ)}', node.pos)
		return ast.PLACEHOLDER_TYPE
	}

	if node.op == .key_not and typ != ast.BOOL_TYPE {
		c.error('cannot use `not` on ${c.table.type_name(typ)}', node.pos)
		return ast.PLACEHOLDER_TYPE
	}

	if node.op == .caret {
		if typ.get_nr_amp() == 0 {
			c.error('cannot dereference ${c.table.type_name(typ)}', node.pos)
			return ast.PLACEHOLDER_TYPE
		}

		return typ.set_nr_amp(typ.get_nr_amp() - 1)
	}

	return typ
}

fun (mut c Checker) selector_expr(mut node ast.SelectorExpr) ast.Type {
	if node.expr is ast.IndexExpr {
		mut expr := node.expr as ast.IndexExpr
		expr.is_selector = true
	}
	node.expr_type = c.expr(node.expr)

	mut sym := c.table.get_sym(node.expr_type)
	if sym.kind == .alias_type {
		sym = c.table.get_sym(sym.parent)
	}

	// TODO interface (especially JS)
	if [ast.TypeKind.struct_, .array, .string, .map].contains(sym.kind) {
		field := sym.find_field(node.field_name, c.table)
		if field.name.length == 0 {
			c.error('${sym.name} has no field ${node.field_name}', node.pos)
			return ast.PLACEHOLDER_TYPE
		}

		if not field.is_pub and sym.pkg != c.pkg {
			c.error('field ${sym.name}.${node.field_name} is private', node.pos)
		}

		return field.typ
	}

	if sym.kind == .sum_type {
		// TODO check if field is present on all variants (with same type)
		c.error('cast to the variant before accessing field of sumtype ${sym.name}', node.pos)
		return ast.VOID_TYPE
	}

	return ast.ANY_TYPE
}

fun (c Checker) string_literal(node ast.StringLiteral) ast.Type {
	return ast.STRING_TYPE
}

fun (mut c Checker) string_inter_literal(node ast.StringInterLiteral) ast.Type {
	for e in node.exprs {
		typ := c.expr(e)
		node.expr_types.push(typ)
	}
	return ast.STRING_TYPE
}

fun (mut c Checker) type_of(mut node ast.TypeOf) ast.Type {
	typ := c.expr(node.expr)
	node.typ = typ
	return ast.STRING_TYPE
}
