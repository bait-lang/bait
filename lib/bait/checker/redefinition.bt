// SPDX-FileCopyrightText: 2024-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package checker

import bait.ast
import bait.errors
import bait.token

// TODO replace this by redefinitions2

enum RedefKind {
	fun_
	type_
}

struct Redefinition {
	path string
	pos token.Pos
	name string
	signature string
}

fun stmt_is_redef(stmt ast.Stmt, name string, kind RedefKind) bool {
	return match kind {
		.fun_ {
			stmt is ast.FunDecl and stmt.name == name
		}
		.type_ {
			(stmt is ast.EnumDecl or stmt is ast.InterfaceDecl or stmt is ast.StructDecl or stmt is ast.TypeDecl) and stmt.name == name
		}
	}
}

fun (mut c Checker) check_redefinitions(redefs_list []string, kind RedefKind, files []ast.File) {
	if redefs_list.length == 0 {
		return
	}

	// Remove duplicates
	mut unique := []string
	for name in redefs_list {
		if not unique.contains(name) {
			unique.push(name)
		}
	}
	// Note: It is assumed that there are at most a few redefinitions.
	// Thus the performance impact of looping over all files and root stmts is ok.
	for name in unique {
		mut redefinitions := []Redefinition
		for file in files {
			for stmt in file.stmts {
				if stmt_is_redef(stmt, name, kind) {
					redefinitions.push(Redefinition{
						path = file.path
						pos = (stmt as ast.InvalidStmt).pos
						name = name
						signature = c.stmt_signature(stmt)
					})
				}
			}
		}

		// Add the error and show where redefinitions happened
		mut err := errors.Message{
			kind = .error
			msg = 'redfinition of "${name}"'
		}
		for redef in redefinitions {
			err.infos.push(errors.Message{
				kind = .info
				path = redef.path
				pos = redef.pos
				title = 'conflict'
				msg = redef.signature

			})
		}
		c.errors.push(err)
	}
}

fun (mut c Checker) check_fun_redefinitions(files []ast.File) {
	c.check_redefinitions(c.redefined_funs, .fun_, files)
}


fun (mut c Checker) check_type_redefinitions(files []ast.File) {
	c.check_redefinitions(c.table.redefined_types, .type_,files)
}

fun (c Checker) stmt_signature(stmt ast.Stmt) string {
	return match stmt {
		ast.EnumDecl { 'enum' }
		ast.FunDecl { c.fun_signature(stmt) }
		ast.InterfaceDecl { 'interface' }
		ast.StructDecl { 'struct' }
		ast.TypeDecl { 'type' }
		else { '' }
	}
}

fun (c Checker) fun_signature(node ast.FunDecl) string {
	mut sig := 'fun ${node.name}('
	for i, p in node.params {
		type_name := c.table.get_sym(p.typ).name
		sig += '${p.name} ${type_name}'
		if i < node.params.length - 1 {
			sig += ', '
		}
	}
	sig += ')'
	if node.return_type != ast.VOID_TYPE {
		sig += ' ${c.table.type_name(node.return_type)}'
	}
	return sig
}
