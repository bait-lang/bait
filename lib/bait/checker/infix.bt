// SPDX-FileCopyrightText: Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package checker

import bait.ast
import bait.token

fun (mut c Checker) infix_expr(mut node ast.InfixExpr) ast.Type {
	node.left_type = c.expr(node.left)
	if node.left_type == ast.ERROR_TYPE {
		return ast.ERROR_TYPE
	}

	c.table.needed_equality_funs.push(node.left_type)

	if c.is_sumtype_match and node.op == .eq {
		node.op = .key_is
	}
	if node.op == .key_is {
		return c.is_sumtype_variant_infix(mut node)
	}

	c.expected_type = node.left_type
	node.right_type = c.expr(node.right)
	if node.right_type == ast.ERROR_TYPE {
		return ast.ERROR_TYPE
	}

	if not c.check_types(node.right_type, node.left_type) {
		mut err_msg := ''
		if node.is_match {
			err_msg = 'cannot match ${c.table.type_name(node.right_type)} to ${c.table.type_name(node.left_type)}'
		} else {
			err_msg = 'infix expr: types ${c.table.type_name(node.left_type)} and ${c.table.type_name(node.right_type)} do not match'
		}
		c.error(err_msg, node.pos)
	}

	if node.left_type == ast.I64_TYPE or node.left_type == ast.U64_TYPE or node.left_type == ast.U32_TYPE {
		node.right = ast.AsCast{
			expr = node.right
			target = node.left_type
		}
	}

	if node.op.is_compare() {
		return ast.BOOL_TYPE
	}

	lsym := c.table.get_sym(node.left_type)
	overload := c.table.get_overload(lsym, node.op.js_repr())
	if overload.name.length > 0 {
		return overload.return_type
	}

	return node.left_type
}

// x is Variant
fun (c Checker) is_sumtype_variant_infix(mut node ast.InfixExpr) ast.Type {
	mut right := node.right as ast.Ident
	if right.pkg == "" and c.pkg != 'main' and c.pkg != "builtin" {
		right.pkg = c.pkg
	}
	node.right_type = c.table.get_idx(right.full_name())

	// Smartcast variable
	if node.left is ast.Ident {
		left := node.left as ast.Ident
		c.scope.update_type(left.full_name(), node.right_type)
		return ast.BOOL_TYPE
	}

	// Smartcast struct field
	if node.left is ast.SelectorExpr {
		left := node.left as ast.SelectorExpr
		name := (left.expr as ast.Ident).full_name() + '.' + left.field_name
		c.scope.update_type(name, node.right_type)
		return ast.BOOL_TYPE
	}

	return ast.BOOL_TYPE
}
