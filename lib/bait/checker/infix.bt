// Copyright (c) 2023-present Lukas Neubert and contributors (see AUTHORS.md).
// This Source Code is subject to the terms of the Mozilla Public License 2.0.
package checker

import bait.ast
import bait.token

fun (c Checker) infix_expr(node ast.InfixExpr) i32 {
	node.left_type = c.expr(node.left)
	if node.op == token.TokenKind.key_is {
		return c.is_sumtype_variant_infix(node)
	}
	node.right_type = c.expr(node.right)
	if not c.check_types(node.right_type, node.left_type) {
		c.error('infix expr: types ${node.right_type} and ${node.left_type} do not match', node.pos)
	}
	if node.op.is_compare() {
		return ast.TypeIdx.bool
	}
	return node.left_type
}

// x is Variant
fun (c Checker) is_sumtype_variant_infix(node ast.InfixExpr) i32 {
	if node.left is ast.Ident {
		mut right := node.right as ast.Ident
		if not right.name.contains('.') {
			right.name = right.pkg + '.' + right.name
		}
		node.right_type = c.table.type_idxs[right.name]
		left := node.left as ast.Ident
		c.scope.update_type(left.name, node.right_type)
	} else if node.left is ast.SelectorExpr {
		mut right := node.right as ast.Ident
		if not right.name.contains('.') {
			right.name = right.pkg + '.' + right.name
		}
		node.right_type = c.table.type_idxs[right.name]
		left := node.left as ast.SelectorExpr
		name := (left.expr as ast.Ident).name + '.' + left.field_name
		c.scope.update_type(name, node.right_type)
	}
	return ast.TypeIdx.bool
}
