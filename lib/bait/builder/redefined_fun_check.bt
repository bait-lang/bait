// Copyright (c) 2023-present Lukas Neubert and contributors (see AUTHORS.md).
// This Source Code is subject to the terms of the Mozilla Public License 2.0.
package builder

import bait.ast
import bait.errors
import bait.token

struct FunRedefinition {
	path string
	pos token.Pos
	name string
	signature string
}

fun (b Builder) check_redefined_functions() {
	// Note: It is assumed that there are at most a few redefined functions.
	// Thus the performance impact of looping over all files and root stmts is negligible.

	// Remove duplicates that happen in case of a tripple redefinition
	mut unique_redefs := []string
	for name in b.table.redefined_funs {
		if not unique_redefs.contains(name) {
			unique_redefs.push(name)
		}
	}

	for name in unique_redefs {
		// Collect information about redefinitions
		mut redefinitions := []FunRedefinition
		for file in b.parsed_files {
			for stmt in file.stmts {
				if stmt is ast.FunDecl and stmt.name == name {
					redefinitions.push(FunRedefinition{
						path = file.path
						pos = stmt.pos
						name = name
						signature = b.fun_signature(stmt)
					})
				}
			}
		}

		// Raise the error and show where redefinitions happened
		errors.generic_error('redfinition of function "${name}"')
		for redef in redefinitions {
			errors.info('conflict', redef.path, redef.pos, redef.signature)
		}
	}
}

fun (b Builder) fun_signature(node ast.FunDecl) string {
	mut sig := 'fun ${node.name}('
	for i, p in node.params {
		type_name := b.table.get_sym(p.typ).name
		sig += '${p.name} ${type_name}'
		if i < node.params.length - 1 {
			sig += ', '
		}
	}
	sig += ')'
	return sig
}
