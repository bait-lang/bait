// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0
package lexer

import bait.token

pub enum CommentMode {
	discard
	store
}

pub struct Lexer {
pub mut:
	val string // Value of the current token
mut:
	comment_mode CommentMode
	text string
	line_starts []i32
	start i32 // Start of the current token
	pos i32 // Position of the next char
	insert_semi bool // Insert a semicolon on \n

	// String interpolation state
	is_str_interpol bool // True for the whole expr (`${...}`)
	is_str_dollar bool // True only for the opening `$`
	str_quote u8
}

pub fun (mut l Lexer) init(text string, comment_mode CommentMode) {
	l.comment_mode = comment_mode
	l.text = text
	l.line_starts = [-1]
	l.pos = 0
}

pub fun (l Lexer) get_pos() token.Pos {
	return token.Pos{
		line = l.line_starts.length
		col = l.start - l.line_starts.last()
	}
}

pub fun (mut l Lexer) next() token.Token {
	l.skip_whitespace()

	// End of file
	if l.pos == l.text.length {
		return .eof
	}

	c := l.text[l.pos]
	l.start = l.pos
	l.pos += 1
	l.insert_semi = false

	// Name or keyword
	if is_name_start_char(c) {
		return l.name_or_key()
	}

	// Number literal
	if c.is_digit() {
		return l.number()
	}

	// String literal or interpolation
	if c == `'` or c == `"` {
		return l.string_val(c)
	}
	if l.is_str_interpol and c == `}` {
		l.is_str_interpol = false
		return l.string_val(l.str_quote)
	}

	// Char
	if c == `\`` {
		return l.char_val()
	}

	// Interoperability (e.g. '#JS')
	if c == `#` {
		l.pos += 1
		l.name_val()
		return .hash
	}

	// Attribute
	if c == `@` {
		l.pos += 1
		l.name_val()
		return .attr
	}

	// Simple tokens
	return l.simple_token(c)
}

fun (mut l Lexer) simple_token(c u8) token.Token {
	// The following tokens are directly converted to string (except comments)
	l.val = ''

	match c {
		`\n` {
			// Only reached if insert_semi was set to true
			l.save_line()
			return .semicolon
		}
		`.` {
			return .dot
		}
		`,` {
			return .comma
		}
		`+` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .plus_assign
			}
			return .plus
		}
		`-` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .minus_assign
			}
			return .minus
		}
		`*` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .mul_assign
			}
			return .mul
		}
		`/` {
			c2 := l.text[l.pos]

			if c2 == `=` {
				l.pos += 1
				return .div_assign
			}

			if c2 == `/` {
				return l.comment()
			}

			return .div
		}
		`%` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .mod_assign
			}
			return .mod
		}
		`=` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .eq
			}
			return .assign
		}
		`:` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .decl_assign
			}
			return .colon
		}
		`!` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .ne
			}
			return .excl
		}
		`<` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .le
			}
			if l.text[l.pos] == `<` {
				l.pos += 1
				return .lshift
			}
			return .lt
		}
		`>` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .ge
			}
			if l.text[l.pos] == `>` {
				l.pos += 1
				return .rshift
			}
			return .gt
		}
		`;` {
			return .semicolon
		}
		`(` {
			return .lpar
		}
		`)` {
			return .rpar
		}
		`[` {
			return .lsqr
		}
		`]` {
			l.insert_semi = true
			return .rsqr
		}
		`{` {
			return .lcur
		}
		`}` {
			return .rcur
		}
		`~` {
			return .tilde
		}
		`&` {
			return .amp
		}
		`|` {
			return .pipe
		}
		`^` {
			return .caret
		}
		`$` {
			if l.is_str_dollar {
				l.is_str_dollar = false
				return .str_dollar
			}
			return .dollar
		}
		else {
			return l.unexpected_char(c)
		}
	}
}

fun (mut l Lexer) name_val() {
	l.start = l.pos - 1
	for l.pos < l.text.length and is_name_char(l.text[l.pos]) {
		l.pos += 1
	}
	l.val = l.text.substr(l.start, l.pos)
}

fun (mut l Lexer) name_or_key() token.Token {
	l.name_val()
	tok := token.kind_from_string(l.val)
	if tok == .key_return {
		l.insert_semi = true
	}
	return tok
}

// FIXME get_pos() should point to start of string (currently: end)
fun (mut l Lexer) string_val(quote u8) token.Token {
	is_foreign := l.text[l.pos - 2] == `.` // E.g. #JS.'eval()'
	l.start = l.pos

	for l.pos < l.text.length {
		c := l.text[l.pos]
		l.pos += 1

		// End of string
		if c == quote {
			l.val = l.text.substr(l.start, l.pos - 1)
			return .string
		}

		if c == `\n` {
			// Properly track line count
			l.save_line()
		} else if c == `\\` {
			// Escape sequence
			l.pos += 1
		} else if not is_foreign and c == `$` and l.text[l.pos] == `{` {
			// Start of interpolated expr
			l.is_str_interpol = true
			l.is_str_dollar = true
			l.str_quote = quote

			l.pos -= 1
			l.val = l.text.substr(l.start, l.pos)
			return .string
		}
	}

	// Unclosed string
	l.start = l.pos
	l.val = 'unclosed string literal'
	return .error
}

fun (mut l Lexer) char_val() token.Token {
	l.start = l.pos
	for l.pos < l.text.length {
		c := l.text[l.pos]
		l.pos += 1
		if c == `\`` {
			break
		}
		if c == `\\` {
			l.pos += 1
		}
	}
	l.val = l.text.substr(l.start, l.pos - 1)
	return .char
}

fun (mut l Lexer) comment() token.Token {
	start := l.pos + 1
	l.skip_line()

	if l.comment_mode == .store {
		// Set val to comment text including all spaces after `//`
		l.val = l.text.substr(start, l.pos - 1)
		return .comment
	}

	// REFAC use non-recursive approch to prevent a growing call stack
	// Discard the comment
	return l.next()
}

fun (mut l Lexer) unexpected_char(c u8) token.Token {
	l.val = 'unexpected char ${c.ascii()}'
	return .error
}

fun (mut l Lexer) save_line() {
	l.line_starts.push(l.pos - 1)
}

fun (mut l Lexer) skip_whitespace() {
	for l.pos < l.text.length {
		c := l.text[l.pos]
		if c == `\n` {
			if l.insert_semi {
				return
			}
			l.pos += 1
			l.save_line()
		} else if c == ` ` or c == `\t` or c == `\r` {
			l.pos += 1
		} else {
			break
		}
	}
}

fun (mut l Lexer) skip_line() {
	for l.pos < l.text.length and l.text[l.pos] != `\n` {
		l.pos += 1
	}
	l.pos += 1
	l.save_line()
}

fun is_name_start_char(c u8) bool {
	return c.is_upper() or c.is_lower() or c == `_`
}

fun is_name_char(c u8) bool {
	return is_name_start_char(c) or c.is_digit()
}
