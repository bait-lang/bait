// SPDX-FileCopyrightText: Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MIT
package lexer

import bait.token

pub enum CommentMode {
	discard
	store
}

pub struct Lexer {
	mut comment_mode CommentMode
	mut text string
	mut insert_semi bool // Insert a semicolon on next \n
	mut val string // Value of the current token

	// Position tracking
	mut line_starts []i32 // Array with positions of the \n before each line
	mut cur_line i32 // Current line number
	mut start i32 // Start of the current token
	mut pos i32 // Always points to the next char in text

	// String interpolation state
	mut is_str_interpol bool // True for the whole expr (`${...}`)
	mut is_str_dollar bool // True only for the opening `$`
	mut str_quote u8
}

pub fun (mut l Lexer) init(text string, comment_mode CommentMode) {
	l.comment_mode = comment_mode
	l.text = text
	l.line_starts = [-1]
	l.cur_line = 0
	l.pos = 0
}

pub fun (l Lexer) offset() i32 {
	return l.pos
}

pub fun (l Lexer) val() string {
	return l.val
}

pub fun (l Lexer) pos() token.Pos {
	return token.Pos{
		line = l.cur_line + 1
		col = l.start - l.line_starts[l.cur_line]
	}
}

pub fun (mut l Lexer) next() token.Token {
	l.skip_whitespace()

	l.start = l.pos

	// End of file
	if l.pos == l.text.length {
		return .eof
	}

	c := l.text[l.pos]
	l.pos += 1
	l.insert_semi = false

	// Name or keyword
	if is_name_start_char(c) {
		return l.name_or_key()
	}

	// Number literal
	if c.is_digit() {
		return l.number()
	}

	// String literal or interpolation
	if c == `'` or c == `"` {
		return l.string_val(c)
	}
	if l.is_str_interpol and c == `}` {
		l.is_str_interpol = false
		return l.string_val(l.str_quote)
	}

	// Char
	if c == `\`` {
		return l.char_val()
	}

	// Interoperability (e.g. '#JS')
	if c == `#` {
		l.pos += 1
		l.name_val()
		return .hash
	}

	// Attribute
	if c == `@` {
		l.pos += 1
		l.name_val()
		return .attr
	}

	// Comments
	if c == `/` and l.text[l.pos] == `/` {
		return l.comment_or_next()
	}

	// Simple tokens
	return l.simple_token(c)
}

fun (mut l Lexer) simple_token(c u8) token.Token {
	// The following tokens are directly converted to string
	l.val = ''

	match c {
		`\n` {
			// Only reached if insert_semi was set to true
			l.inc_line()
			return .semicolon
		}
		`.` {
			if l.text[l.pos] == `.` {
				l.pos += 1
				return .dotdot
			}
			return .dot
		}
		`,` {
			return .comma
		}
		`+` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .plus_assign
			}
			return .plus
		}
		`-` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .minus_assign
			}
			return .minus
		}
		`*` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .mul_assign
			}
			return .mul
		}
		`/` {
			if l.text[l.pos] == `=`  {
				l.pos += 1
				return .div_assign
			}
			return .div
		}
		`%` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .mod_assign
			}
			return .mod
		}
		`=` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .eq
			}
			return .assign
		}
		`:` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .decl_assign
			}
			return .colon
		}
		`!` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .ne
			}
			return .excl
		}
		`<` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .le
			}
			if l.text[l.pos] == `<` {
				l.pos += 1
				return .lshift
			}
			return .lt
		}
		`>` {
			if l.text[l.pos] == `=` {
				l.pos += 1
				return .ge
			}
			if l.text[l.pos] == `>` {
				l.pos += 1
				return .rshift
			}
			return .gt
		}
		`;` {
			return .semicolon
		}
		`(` {
			return .lpar
		}
		`)` {
			return .rpar
		}
		`[` {
			return .lsqr
		}
		`]` {
			l.insert_semi = true
			return .rsqr
		}
		`{` {
			return .lcur
		}
		`}` {
			return .rcur
		}
		`~` {
			return .tilde
		}
		`&` {
			return .amp
		}
		`|` {
			return .pipe
		}
		`^` {
			return .caret
		}
		`$` {
			if l.is_str_dollar {
				l.is_str_dollar = false
				return .str_dollar
			}
			return .dollar
		}
		else {
			return l.unexpected_char(c)
		}
	}
}

fun (mut l Lexer) name_val() {
	l.start = l.pos - 1
	for l.pos < l.text.length and is_name_char(l.text[l.pos]) {
		l.pos += 1
	}
	l.val = l.text.substr(l.start, l.pos)
}

fun (mut l Lexer) name_or_key() token.Token {
	l.name_val()
	tok := token.kind_from_string(l.val)
	if tok == .key_return or tok == .key_break or tok == .key_continue {
		l.insert_semi = true
	}
	return tok
}

fun (mut l Lexer) string_val(quote u8) token.Token {
	is_foreign := l.text[l.pos - 2] == `.` // E.g. #JS.'eval()'
	l.start = l.pos

	for l.pos < l.text.length {
		c := l.text[l.pos]
		l.pos += 1

		// End of string
		if c == quote {
			l.val = l.text.substr(l.start, l.pos - 1)
			return .string
		}

		if c == `\n` {
			// Properly track line count
			l.save_line()
		} else if c == `\\` {
			// Escape sequence
			l.pos += 1
		} else if not is_foreign and c == `$` and l.text[l.pos] == `{` {
			// Start of interpolated expr
			l.is_str_interpol = true
			l.is_str_dollar = true
			l.str_quote = quote

			l.pos -= 1
			l.val = l.text.substr(l.start, l.pos)
			return .string
		}
	}

	// Unclosed string
	l.val = 'unclosed string literal [L0010]'
	return .error
}

fun (mut l Lexer) char_val() token.Token {
	l.start = l.pos
	for l.pos < l.text.length {
		c := l.text[l.pos]
		l.pos += 1
		if c == `\`` {
			break
		}
		if c == `\\` {
			l.pos += 1
		}
	}
	l.val = l.text.substr(l.start, l.pos - 1)
	return .char
}

fun (mut l Lexer) unexpected_char(c u8) token.Token {
	l.val = 'unexpected char ${c.ascii()} [L0001]'
	return .error
}

fun (mut l Lexer) inc_line() {
	l.save_line()
	l.cur_line = l.line_starts.length - 1
}

fun (mut l Lexer) save_line() {
	l.line_starts.push(l.pos - 1)
}

fun (mut l Lexer) skip_whitespace() {
	for l.pos < l.text.length {
		c := l.text[l.pos]
		if c == `\n` {
			// \n as semicolon is handled in simple_token()
			if l.insert_semi {
				return
			}

			l.pos += 1
			l.inc_line()
		} else if c == ` ` or c == `\t` or c == `\r` {
			l.pos += 1
		} else {
			break
		}
	}
}

fun is_name_start_char(c u8) bool {
	return c.is_upper() or c.is_lower() or c == `_`
}

fun is_name_char(c u8) bool {
	return is_name_start_char(c) or c.is_digit()
}
