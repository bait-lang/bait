// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0
package js

import bait.ast
import bait.errors
import bait.token
import bait.util

fun (mut g Gen) stmts(stmts []ast.Stmt) {
	g.indent += 1
	for stmt in stmts {
		g.stmt(stmt)
	}
	g.indent -= 1
}

fun (mut g Gen) stmt(stmt ast.Stmt) {
	if stmt is ast.AssertStmt {
		g.assert_stmt(stmt)
	} else if stmt is ast.AssignStmt {
		g.assign_stmt(stmt)
	} else if stmt is ast.ConstDecl {
		g.const_decl(stmt)
	} else if stmt is ast.ExprStmt {
		g.save_stmt_offset()
		g.expr(stmt.expr)
		if not g.empty_line {
			g.writeln("")
		}
	} else if stmt is ast.EnumDecl {
		g.enum_decl(stmt)
	} else if stmt is ast.ForLoop {
		g.for_loop(stmt)
	} else if stmt is ast.ForClassicLoop {
		g.for_classic_loop(stmt)
	} else if stmt is ast.ForInLoop {
		g.for_in_loop(stmt)
	} else if stmt is ast.FunDecl {
		g.fun_decl(stmt)
	} else if stmt is ast.GlobalDecl {
		g.global_decl(stmt)
	} else if stmt is ast.IfMatch {
		g.if_match(stmt)
	} else if stmt is ast.InterfaceDecl {
		g.interface_decl(stmt)
	} else if stmt is ast.LoopControlStmt {
		g.loop_control_stmt(stmt)
	} else if stmt is ast.ReturnStmt {
		g.return_stmt(stmt)
	} else if stmt is ast.StructDecl {
		g.struct_decl(stmt)
	} else if stmt is ast.TypeDecl {
		g.type_decl(stmt)
	} else {}
}

fun (mut g Gen) assert_stmt(node ast.AssertStmt) {
	g.write('if (')
	g.expr(node.expr)
	g.writeln(') {')
	g.writeln('\tTestRunner_assert_pass(test_runner)')
	g.writeln('} else {')
	if node.expr is ast.InfixExpr {
		expr := node.expr as ast.InfixExpr
		g.write('\tTestRunner_set_assert_info(test_runner, ${node.pos.line}, from_js_string("assert ')
		g.assert_side_expr(expr.left)
		g.write(' ${expr.op.js_repr()} ')
		g.assert_side_expr(expr.right)
		g.write('"), ')
		g.expr_to_string(expr.left, expr.left_type)
		g.write(', ')
		g.expr_to_string(expr.right, expr.right_type)
		g.writeln(')')
		g.writeln('\tTestRunner_assert_fail_infix(test_runner)')
	} else {
		g.write('\tTestRunner_set_assert_info(test_runner, ${node.pos.line}, from_js_string("assert ')
		g.assert_side_expr(node.expr)
		g.writeln('"), from_js_string(""), from_js_string(""), from_js_string(""))')
		g.writeln('\tTestRunner_assert_fail(test_runner)')
	}
	g.writeln('}')
}

fun (mut g Gen) assert_side_expr(node ast.Expr) {
	if node is ast.BoolLiteral {
		g.bool_literal(node)
	} else if node is ast.IntegerLiteral {
		g.integer_literal(node)
	} else if node is ast.StringLiteral {
		g.write("'")
		g.write(util.escape_linebreak(node.val))
		g.write("'")
	} else if node is ast.CharLiteral {
		g.write("`")
		g.write(node.val)
		g.write("`")
	} else if node is ast.ArrayInit {
		g.write('[')
		for i, expr in node.exprs {
			g.assert_side_expr(expr)
			if i < node.exprs.length - 1 {
				g.write(', ')
			}
		}
		g.write(']')
	} else if node is ast.Ident {
		g.write(node.name)
	} else if node is ast.ArrayInit {
		g.write('[')
		for i, expr in node.exprs {
			g.assert_side_expr(expr)
			if i < node.exprs.length - 1 {
				g.write(', ')
			}
		}
		g.write(']')
	} else if node is ast.IndexExpr {
		g.assert_side_expr(node.left)
		g.write('[')
		g.assert_side_expr(node.index)
		g.write(']')
	} else if node is ast.SelectorExpr {
		g.assert_side_expr(node.expr)
		g.write('.${node.field_name}')
	} else if node is ast.ComptimeVar {
		g.write('$')
		g.write(node.name)
	} else {
		g.write('UNHANDLED EXPR')
	}
}

fun (mut g Gen) assign_stmt(node ast.AssignStmt){
	if node.op == .decl_assign {
		left := node.left as ast.Ident
		if left.is_mut {
			g.write('let ')
		} else {
			g.write('const ')
		}
	}

	g.is_lhs_assign = true
	g.expr(node.left)
	g.is_lhs_assign = false

	lsym := g.table.get_sym(node.left_type)
	if lsym.overloads.contains(node.op.js_repr()) {
		g.write(' = ')
		overload := lsym.overloads[node.op.js_repr()]
		g.write(js_name(lsym.name + '_' + overload.name))
		g.write('(')
		g.expr(node.left)
		g.write(', ')
		g.expr(node.right)
		g.writeln(')')
		return
	}

	if g.is_array_map_set {
		g.expr(node.right)
		g.writeln(')')
		g.is_array_map_set = false
		return
	}

	g.write(' ')
	g.write(node.op.js_repr())
	g.write(' ')
	g.expr(node.right)
	if not g.is_for_loop_head {
		g.writeln('')
	}
}

fun (mut g Gen) const_decl(node ast.ConstDecl){
	if node.lang != .bait {
		return
	}

	g.write('const ')
	g.write(js_name(node.name))
	g.write(' = ')
	g.expr(node.expr)
	g.writeln('')
}

fun (mut g Gen) enum_decl(node ast.EnumDecl){
	if node.lang != .bait {
		return
	}

	g.writeln('const ' + js_name(node.name) + ' = {')
	g.indent += 1
	for field in node.fields {
		g.write('${field.name}: ')
		g.expr(field.expr)
		g.writeln(',')
	}
	g.indent -= 1
	g.writeln('}')
}

fun (mut g Gen) loop_control_stmt(node ast.LoopControlStmt){
	g.write(node.kind.js_repr())
	if node.label.length > 0 {
		g.writeln(' ${node.label}')
	} else {
		g.writeln('')
	}
}

fun (mut g Gen) write_label(label string){
	if label.length > 0 {
		g.write('${label}: ')
	}
}

fun (mut g Gen) for_loop(node ast.ForLoop){
	g.write_label(node.label)

	g.write('while (')
	g.expr(node.cond)
	g.writeln(') {')
	g.stmts(node.stmts)
	g.writeln('}')
}

fun (mut g Gen) for_classic_loop(node ast.ForClassicLoop){
	g.write_label(node.label)

	g.is_for_loop_head = true
	g.write('for (')
	g.stmt(node.init)
	g.write('; ')
	g.expr(node.cond)
	g.write('; ')
	g.stmt(node.inc)
	g.writeln(') {')
	g.is_for_loop_head = false
	g.stmts(node.stmts)
	g.writeln('}')
}

fun (mut g Gen) for_in_loop(node ast.ForInLoop){
	g.write_label(node.label)

	mut i := js_name(node.idxvar)
	if i == '' {
		i = g.new_temp_var()
	}
	sym := g.table.get_sym(node.expr_type)
	if sym.kind == .map {
		g.for_in_map(node)
		return
	}
	container := g.expr_string(node.expr)
	g.writeln('for (let ${i} = 0; ${i} < ${container}.length; ${i}++) {')
	val_name := js_name(node.valvar.name)
	if sym.kind == .array {
		g.writeln('\tconst ${val_name} = Array_get(${container}, ${i})')
	} else if sym.kind == .string {
		g.writeln('\tconst ${val_name} = string_get(${container}, ${i})')
	} else {
		g.writeln('\tconst ${val_name} = ${container}[${i}]')
	}
	g.stmts(node.stmts)
	g.writeln('}')
}

fun (mut g Gen) for_in_map(node ast.ForInLoop){
	container := g.expr_string(node.expr)
	keys_var := g.new_temp_var()
	g.writeln('const ${keys_var} = map_keys(${container})')
	i := g.new_temp_var()
	g.writeln('for (let ${i} = 0; ${i} < ${keys_var}.length; ${i}++) {')
	g.writeln('\tconst ${js_name(node.idxvar)} = Array_get(${keys_var}, ${i})')
	g.writeln('\tconst ${js_name(node.valvar.name)} = map_get(${container}, ${node.idxvar})')
	g.stmts(node.stmts)
	g.writeln('}')
}

fun (mut g Gen) global_decl(node ast.GlobalDecl){
	name := js_name(node.name)
	expr := g.expr_string(node.expr)
	g.global_out += 'var ${name} = ${expr}\n'
}

fun (mut g Gen) interface_decl(node ast.InterfaceDecl){
	if node.lang != .bait {
		return
	}
	errors.error(g.path, node.pos, 'Bait interfaces are not yet supported')
	exit(1)
}

fun (mut g Gen) return_stmt(node ast.ReturnStmt){
	g.save_stmt_offset()

	g.write('return')
	if not (node.expr is ast.EmptyExpr) {
		g.write(" ")
		g.expr(node.expr)
	}
	g.writeln('')
}

fun (mut g Gen) type_decl(node ast.TypeDecl){
	sym := g.table.get_sym(node.typ)
	if sym.kind != .alias_type {
		return
	}

	g.write('function ')
	g.write(js_name(node.name))
	g.writeln('(val) { return val }')
}
