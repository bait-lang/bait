// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0
package js

import bait.ast

fun (mut g Gen) fun_decl(node ast.FunDecl) {
	if node.lang != .bait {
		return
	}

	if node.name == 'testsuite_begin' {
		g.has_test_begin = true
	} else if node.name == 'testsuite_end' {
		g.has_test_end = true
	}

	if node.generic_names.length > 0 and g.cur_concrete_types.length == 0 {
		gtypes := g.table.generic_fun_types[node.key()]
		for conc_types in gtypes {
			for i, gn in node.generic_names {
				g.cur_concrete_types[gn] = conc_types[i]
			}
			g.fun_decl(node)
			g.cur_concrete_types.clear()
		}

		return
	}

	g.cur_fun = node
	g.write('function ')

	mut name := ''
	if node.is_method {
		sym := g.table.get_sym(node.params[0].typ)
		name = js_name(sym.name + '_' + node.name)
	} else {
		name = js_name(node.name)
	}
	if g.cur_concrete_types.length > 0 {
		name = g.get_concrete_name(name, g.cur_concrete_types.values())
	}

	g.write('${name}(')
	g.fun_params(node.params)
	g.writeln(') {')
	g.stmts(node.stmts)
	g.writeln('}')

	export_attr := node.attrs.find_attr('export')
	if export_attr.name != '' {
		g.writeln('module.exports.${export_attr.value} = ${js_name(node.name)}')
	}

	g.writeln('')
}

fun (mut g Gen) fun_params(params []ast.Param){
	for i, p in params {
		g.write(js_name(p.name))
		if i < params.length - 1 {
			g.write(", ")
		}
	}
}

fun (mut g Gen) call_expr(node ast.CallExpr) {
	if node.or_block.kind == .none {
		g.call_expr_no_or(node)
		return
	}

	// Or block
	cut := g.cut_back_to(g.stmt_offsets.last())
	g.empty_line = true
	tmp := g.new_temp_var()
	g.write('let ${tmp} = ')

	g.call_expr_no_or(node)
	g.writeln('')

	g.writeln('if (${tmp}.is_error) {')
	if node.or_block.kind == .block {
		g.writeln('\t const err = ${tmp}.msg')
		g.stmts(node.or_block.stmts)

		if node.or_block.stmts.last() is ast.ExprStmt {
			if g.empty_line {
				g.cut_before(`\n`)
			}
			last_line_cut := g.cut_before(`\t`)

			g.writeln('${tmp}.data = ${last_line_cut}')
		}
	} else {
		if g.cur_fun.is_main() {
			// TODO test
			g.writeln('\tpanic(${tmp}.msg)')
		} else {
			g.writeln('\treturn ${tmp}')
		}
	}

	g.writeln('}')
	g.save_stmt_offset()

	g.empty_line = false
	g.write('${cut}${tmp}.data')
}

fun (mut g Gen) call_expr_no_or(node ast.CallExpr) {
	if node.is_method and node.lang != .bait {
		g.expr(node.left)
		g.write('.' + node.name + '(')
		g.call_args(node.args)
		g.write(')')
		return
	}

	if node.is_field {
		g.expr(node.left)
		g.write('.')
		g.write(node.name)
		g.write('(')
		g.call_args(node.args)
		g.write(')')
		return
	}

	mut name := node.name
	if node.is_method {
		if name == 'str' {
			g.expr_to_string(node.left, node.left_type)
			return
		}

		sym := g.table.get_sym(node.left_type)
		final_sym := g.table.get_final_sym(sym)
		if final_sym.kind == .array and ['push', 'push_many', 'push_many_with_len'].contains(node.name) {
			g.gen_array_method(node.name, node, final_sym)
			return
		}

		name = js_name(sym.name + '_' + node.name)
	} else if node.lang == .bait{
		name = js_name(node.name)
	}

	if node.concrete_types.length > 0 {
		name = g.get_concrete_name(name, node.concrete_types)
	}

	g.write(name)

	if ['println', 'eprintln', 'print', 'eprint'].contains(node.name) {
		g.write('(')
		g.expr_to_string(node.args[0].expr, node.args[0].typ)
		g.write(')')
		return
	}
	g.write('(')
	if node.is_method {
		g.expr(node.left)
		if node.args.length > 0 {
			g.write(', ')
		}
	}
	g.call_args(node.args)
	g.write(')')
}

fun (mut g Gen) call_args(args []ast.CallArg) {
	for i, a in args {
		g.expr(a.expr)
		if i < args.length - 1 {
			g.write(", ")
		}
	}
}

fun (mut g Gen) gen_array_method(name string, node ast.CallExpr, sym ast.TypeSymbol) {
	g.write('Array_${name}(')
	g.expr(node.left)
	g.write(', ')
	g.expr(node.args[0].expr)
	for i := 1; i < node.args.length; i += 1 {
		g.write(', ')
		g.expr(node.args[i].expr)
	}
	g.write(')')
}
