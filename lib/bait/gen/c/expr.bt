// Copyright (c) 2023-present Lukas Neubert and contributors (see AUTHORS.md).
// This Source Code is subject to the terms of the Mozilla Public License 2.0.
package c

import bait.ast
import bait.errors
import bait.util

fun (g Gen) expr(expr ast.Expr) {
	match expr {
		ast.CallExpr { g.call_expr(expr) }
		ast.Ident { g.ident(expr) }
		ast.IfExpr { g.if_expr(expr) }
		ast.InfixExpr { g.infix_expr(expr) }
		ast.IntegerLiteral { g.integer_literal(expr) }
		ast.SelectorExpr { g.selector_expr(expr) }
		ast.StringLiteral { g.string_literal(expr) }
		ast.StructInit { g.struct_init(expr) }
		else {}
	}
}

fun (g Gen) call_expr(node ast.CallExpr) {
	// TODO method
	if node.lang == .bait{
		g.write(c_name(node.name))
	} else {
		g.write(node.name.replace('C.', ''))
	}
	if ['println', 'eprintln'].contains(node.name) {
		g.write('(')
		g.expr_to_string(node.args[0].expr, node.args[0].typ)
		g.write(')')
		return
	}
	g.write('(')
	// TODO method receiver
	g.call_args(node.args)
	g.write(')')
}

fun (g Gen) call_args(args []ast.CallArg) {
	for i, a in args {
		g.expr(a.expr)
		if i < args.length - 1 {
			g.write(", ")
		}
	}
}

fun (g Gen) ident(node ast.Ident) {
	g.write(c_name(node.name))
}

fun (g Gen) if_expr(node ast.IfExpr) {
	for i, b in node.branches {
		if i > 0 {
			g.write('} else ')
		}
		if node.has_else and i == node.branches.length - 1 {
			g.writeln('{')
		} else {
			g.write('if (')
			g.expr(b.cond)
			g.writeln(') {')
		}
		g.stmts(b.stmts)
	}
	g.writeln('}')
}

fun (g Gen) infix_expr(node ast.InfixExpr) {
	// Overloading
	lsym := g.table.get_sym(node.left_type)
	if lsym.overloads.contains(node.op.js_repr()) {
		overload := lsym.overloads[node.op.js_repr()]
		g.write(c_name(lsym.name + '_' + overload.name))
		g.write('(')
		g.expr(node.left)
		g.write(', ')
		g.expr(node.right)
		g.write(')')
		return
	}

	// Normal cases
	g.expr(node.left)
	g.write(' ')
	g.write(node.op.js_repr())
	g.write(' ')
	g.expr(node.right)
}

fun (g Gen) integer_literal(node ast.IntegerLiteral) {
	g.write(node.val)
}

fun (g Gen) selector_expr(node ast.SelectorExpr) {
	g.expr(node.expr)
	g.write('.')
	g.write(node.field_name)
}

fun (g Gen) string_literal(node ast.StringLiteral) {
	val := util.escape_char(node.val.replace('\n', '\\n'), `\"`)
	g.write('from_c_string("')
	g.write(val)
	g.write('")')
}

fun (g Gen) struct_init(node ast.StructInit) {
	type_str := g.typ(node.typ)
	g.write('(${type_str}){')

	mut inited_fields := []string
	for field in node.fields {
		inited_fields.push(field.name)
	}

	info := g.table.get_sym(node.typ).info as ast.StructInfo
	for i, field in info.fields {
		name := c_name(field.name)
		g.write('.${name} = ')
		init_idx := inited_fields.index(field.name)
		if init_idx == -1 {
			g.write('0')
		} else {
			g.expr(node.fields[init_idx].expr)
		}
		if i < info.fields.length - 1 {
			g.write(', ')
		}
	}

	if info.fields.length == 0 {
		g.write('0')
	}

	g.write('}')
}

fun (g Gen) expr_to_string(expr ast.Expr, typ ast.Type) {
	sym := g.table.get_sym(typ)
	if sym.kind == .string {
		g.expr(expr)
		return
	}

	str_def := g.table.get_method(sym, 'str')
	if str_def.name.length > 0 {
		final_sym := g.table.get_sym(str_def.params[0].typ)
		mut name := c_name(final_sym.name)
		g.write('${name}_str(')
		g.expr(expr)
		g.write(')')
		return
	}

	e := expr as ast.EmptyExpr
	errors.error(g.path, expr.pos, 'cannot convert ${sym.name} to string')
	exit(1)
}
