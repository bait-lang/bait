// Copyright (c) 2023-present Lukas Neubert.
// This Source Code is subject to the terms of the Mozilla Public License 2.0.
package c

import bait.ast
import bait.errors

fun (mut g Gen) stmts(stmts []ast.Stmt) {
	g.indent += 1
	for stmt in stmts {
		g.stmt(stmt)
	}
	g.indent -= 1
}

fun (mut g Gen) stmt(stmt ast.Stmt) {
	match stmt {
		ast.AssignStmt { g.assign_stmt(stmt) }
		ast.ConstDecl { g.const_decl(stmt) }
		ast.ExprStmt { g.expr_stmt(stmt) }
		ast.ForLoop { g.for_loop(stmt) }
		ast.ForClassicLoop { g.for_classic_loop(stmt) }
		ast.ForInLoop { g.for_in_loop(stmt) }
		ast.FunDecl { g.fun_decl(stmt) }
		ast.InterfaceDecl { g.interface_decl(stmt) }
		ast.ReturnStmt { g.return_stmt(stmt) }
		ast.StructDecl {} // see write_types()
		ast.TypeDecl {} // see write_types()
		else {}
	}
}

fun (mut g Gen) assign_stmt(node ast.AssignStmt) {
	if node.op == .decl_assign {
		type_str := g.typ(node.right_type)
		g.write('${type_str} ')
	} else if node.left_type.get_nr_amp() > 0 and node.left is ast.Ident {
		g.write('*')
	}

	g.is_lhs_assign = true
	g.expr(node.left)
	g.is_lhs_assign = false

	lsym := g.table.get_sym(node.left_type)
	if lsym.overloads.contains(node.op.c_repr()) {
		g.write(' = ')
		overload := lsym.overloads[node.op.c_repr()]
		g.write(c_name(lsym.name + '_' + overload.name))
		g.write('(')
		g.expr(node.left)
		g.write(', ')
		g.expr(node.right)
		g.writeln(');')
		return
	}

	if g.is_array_map_set {
		g.write('{')
		g.expr(node.right)
		g.writeln('});')
		g.is_array_map_set = false
		return
	}

	g.write(' ')
	g.write(node.op.c_repr())
	g.write(' ')
	g.expr(node.right)
	if not g.is_for_loop_head {
		g.writeln(';')
	}
}

fun (mut g Gen) const_decl(node ast.ConstDecl){
	if node.lang != .bait {
		return
	}

	name := c_name(node.name)
	val := g.expr_string(node.expr)

	if node.expr is ast.ArrayInit or node.expr is ast.CallExpr or node.expr is ast.MapInit {
		typ := g.typ(node.typ)
		g.type_defs_out += '${typ} ${name};\n'
		g.main_inits_out += '\t${name} = ${val};\n'
	} else {
		g.type_impls_out += '#define ${name} ${val}\n'
	}
}

fun (mut g Gen) expr_stmt(node ast.ExprStmt) {
	g.expr(node.expr)
	if not g.empty_line {
		g.writeln(";")
	}
}

fun (mut g Gen) write_label(label string){
	if label.length > 0 {
		g.writeln('${label}: ')
	}
}

fun (mut g Gen) for_loop(node ast.ForLoop) {
	g.write('while (')
	g.expr(node.cond)
	g.writeln(') {')
	g.stmts(node.stmts)
	g.writeln('}')

	g.write_label(node.label)
}

fun (mut g Gen) for_classic_loop(node ast.ForClassicLoop) {
	g.is_for_loop_head = true
	g.write('for (')
	g.stmt(node.init)
	g.write('; ')
	g.expr(node.cond)
	g.write('; ')
	g.stmt(node.inc)
	g.writeln(') {')
	g.is_for_loop_head = false
	g.stmts(node.stmts)
	g.writeln('}')

	g.write_label(node.label)
}

fun (mut g Gen) for_in_loop(node ast.ForInLoop){
	mut i := node.idxvar
	if i == '' {
		i = g.new_temp_var()
	}

	sym := g.table.get_sym(node.expr_type)
	container := g.expr_string(node.expr)
	g.writeln('for (i32 ${i} = 0; ${i} < ${container}.length; ${i}++) {')
	if sym.kind == .array {
		info := sym.info as ast.ArrayInfo
		typ := g.typ(info.elem_type)
		g.writeln('\t${typ} ${node.valvar.name} = (*(${typ}*)(Array_get(${container}, ${i})));')
	} else if sym.kind == .string {
		g.writeln('\tu8 ${node.valvar.name} = string_get(${container}, ${i});')
	}
	// TODO for in map
	g.stmts(node.stmts)
	g.writeln('}')
}

fun (g Gen) interface_decl(node ast.InterfaceDecl){
	if node.lang != .bait {
		return
	}
	errors.error(g.path, node.pos, 'Bait interfaces are not yet supported')
	exit(1)
}

fun (mut g Gen) return_stmt(node ast.ReturnStmt){
	g.write('return')
	if not (node.expr is ast.EmptyExpr) {
		g.write(" ")
		g.expr(node.expr)
	}
	g.writeln(';')
}
