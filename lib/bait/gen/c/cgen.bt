// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0
package c

import bait.preference
import bait.ast
import bait.errors

const C_RESERVED := [
	// C99 reserved keywords. See https://en.cppreference.com/w/c/keyword
	'auto', 'break', 'case', 'char', 'const', 'continue', 'default', 'do', 'double', 'else', 'enum', 'extern', 'float', 'for', 'goto', 'if', 'inline', 'int', 'long', 'register', 'restrict', 'return', 'short', 'signed', 'sizeof', 'static', 'struct', 'switch', 'typedef', 'union', 'unsigned', 'void', 'volatile', 'while',
	// Builtin function names
	'main', 'exit',
]

struct Gen {
	pref preference.Prefs
	table ast.Table
mut:
	path string
	pkg string
	main_inits_out string
	type_defs_out string
	fun_decls_out string
	type_impls_out string
	globals_out string
	out string
	indent i32 := -1
	empty_line bool := true
	stmt_offsets []i32
	foreign_imports []string // C header files
	generated_eq_funs []ast.Type
	tmp_counter i32
	cur_concrete_types map[string]ast.Type
	cur_fun ast.FunDecl
	is_lhs_assign bool
	is_array_map_set bool
	is_for_loop_head bool
	// Testing
	has_test_begin bool
	has_test_end bool
	// Cached comptime variables
	baitexe string
	baitdir string
	baithash string
}

pub fun gen(files []ast.File, table ast.Table, pref preference.Prefs) string {
	mut g := Gen{
		pref = pref
		table = table
	}

	g.write_types()
	g.gen_equality_funs()

	for file in files {
		g.path = file.path
		g.pkg = file.pkg_decl.full_name

		g.process_imports(file.imports)
		g.stmts(file.stmts)
		g.out += "\n"
	}
	if pref.is_test {
		g.gen_test_main()
	}
	if not g.pref.is_library {
		g.c_main()
	}
	return g.headers() + g.type_defs_out + g.fun_decls_out + g.type_impls_out + g.globals_out + '\n' + g.out
}

fun (g Gen) process_imports(imports []ast.Import) {
	for imp in imports {
		// TODO handle alias clash
		if imp.lang == .bait or g.foreign_imports.contains(imp.name) {
			continue
		}
		g.foreign_imports.push(imp.name)
	}
}

fun (g Gen) headers() string {
	mut headers := ''
	for name in g.foreign_imports {
		headers += '#include <${name}>\n'
	}
	return headers + '\n'
}

fun (mut g Gen) gen_test_main() {
	mut nr_test_funs := 0
	g.writeln('int bait_main() {')
	g.indent += 2
	if g.has_test_begin {
		g.writeln('testsuite_begin();')
	}
	for key, func in g.table.fun_decls {
		if func.is_test {
			nr_test_funs += 1
			name := c_name(func.name)
			esc_path := g.path.replace('\\', '\\\\')
			g.writeln('TestRunner_set_test_info(&test_runner, from_c_string("${esc_path}"), from_c_string("${name}"));')
			g.writeln('${name}();')
		}
	}
	if g.has_test_end {
		g.writeln('testsuite_end();')
	}
	g.writeln('exit(TestRunner_exit_code(test_runner));')
	g.indent -= 2
	g.writeln('}\n')

	if nr_test_funs == 0 {
		errors.generic_error('${g.path} contains no tests')
		exit(1)
	}
}

fun (mut g Gen) c_main() {
	g.writeln('int main(int argc, char* argv[]) {')
	g.writeln(g.main_inits_out)
	g.writeln('\tbait_main();')
	g.writeln('\treturn 0;')
	g.writeln('}')
}


fun (g Gen) get_concrete_name(name string, concrete_types []ast.Type) string {
	mut full_name := name
	for t in concrete_types {
		full_name += '_' + g.table.get_sym(t).name
	}
	return c_name(full_name)
}

fun (mut g Gen) new_temp_var() string {
	g.tmp_counter += 1
	return '_t${g.tmp_counter}'
}

fun (mut g Gen) write_indent() {
	if g.indent > 0 and g.empty_line {
		g.out += '\t'.repeat(g.indent)
	}
}

fun (mut g Gen) write(s string){
	g.write_indent()
	g.out += s
	g.empty_line = false
}

fun (mut g Gen) writeln (s string){
	g.write_indent()
	g.out += s + "\n"
	g.empty_line = true
}

fun (mut g Gen) cut_before(to u8) string {
	for i := g.out.length - 1; i >= 0; i -= 1 {
		if g.out[i] == to {
			mut cut := g.out.substr(i + 1, g.out.length)
			g.out = g.out.substr(0, i)
			return cut
		}
	}
	return g.out
}

fun (mut g Gen) cut_back_to(pos i32) string {
	cut := g.out.substr(pos, g.out.length)
	g.out = g.out.substr(0, pos)
	return cut
}

fun (mut g Gen) save_stmt_offset() {
	g.stmt_offsets.push(g.out.length)
}

fun c_name(n string) string {
	name := n.replace('.', '__').replace('[]', 'Array_')
	if C_RESERVED.contains(name) {
		return 'bait_${name}'
	}
	return name
}
