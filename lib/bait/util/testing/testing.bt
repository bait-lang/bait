// Copyright (c) 2023-present Lukas Neubert and contributors (see AUTHORS.md).
// This Source Code is subject to the terms of the Mozilla Public License 2.0.
package testing

import os

pub fun build_file(dir string, file string) i32 {
	cmd := 'node ${$BAITEXE} ${dir}/${file}'
	res := os.exec(cmd)

	if res.code == 0 {
		println('OK ${file}')
		return 0
	}

	eprintln('FAIL ${file}')
	eprintln(res.stderr)
	return 1
}

pub fun build_files_in_dir(dir string) {
	mut fails := 0
	files := os.ls(dir)

	for file in files {
		if not file.ends_with('.bt') {
			continue
		}

		fails += build_file(dir, file)
	}

	if fails > 0 {
		exit(1)
	}
}

pub fun in_out_runner(dir string, options string) {
	in_files := os.walk_ext(dir, '.in.bt')

	for file in in_files {
		mut in_path := file
		name_ext := os.file_name(file)
		name := name_ext.all_before('.in.bt')

		out_file := '${dir}/${name}.out'
		if not os.exists(out_file) {
			eprintln('out file ${out_file} does not exist')
			assert false
			continue
		}

		mut cmd := 'build'
		is_test := name_ext.contains('_test.in.bt')
		if is_test {
			cmd = 'test'
			in_path = file.replace('.in', '')
			os.cp(file, in_path)
		}

		expected := os.read_file(out_file)
		res := os.exec('node ${$BAITEXE} --nocolor ${options} ${cmd} ${in_path}')
		assert expected == res.stderr

		if is_test {
			os.rm(in_path)
		}
	}
}
