// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0
package testing

import os

pub struct InOutRunner {
	check_stdout bool // Compare expected to stdout instead of stderr
global:
	root string // Root directory to search tests in
	cmd_options string // Additional cmd options for compiling the tests
	skips []string
}

pub fun (runner InOutRunner) test_dir(dir string) {
	if runner.skips.contains(dir) {
		eprintln('skipping ${dir}')
		return
	}

	abs_dir := os.join_path(runner.root, [dir])
	if not os.is_dir(abs_dir) {
		return
	}

	res := os.exec('node ${$BAITEXE} --nocolor ${runner.cmd_options} run ${abs_dir}')
	if res.code != 0 {
		eprintln('compilation failed: ${res.stderr}')
		assert false
		return
	}

	expected := read_out_file('${abs_dir}/expected.out').replace('\r\n', '\n')
	runner.compare_expected(res, expected)
}

pub fun (runner InOutRunner) test_all_in_root() {
	dirs := os.ls(runner.root)
	for dir in dirs {
		runner.test_dir(dir)
	}
}

fun (runner InOutRunner) compare_expected(res os.Result, expected string) {
	if runner.check_stdout {
		res := normalize(res.stdout)
		assert res == expected
	} else {
		res := normalize(res.stderr)
		assert res == expected
	}
}

pub fun in_out_runner(runner InOutRunner) {
	in_files := os.walk_ext(runner.root, '.bt')

	for file in in_files {
		// Warn if a file lacks the .in.bt extension. This might be a mistake
		if not file.ends_with('.in.bt') {
			eprintln('warning: file ${file} does not end with .in.bt')
			continue
		}

		if runner.skips.contains(file) {
			eprintln('skipping ${file}')
			continue
		}

		name_ext := os.file_name(file)

		mut cmd := 'build'
		if runner.check_stdout {
			cmd = 'run'
		}
		if name_ext.contains('_test.') {
			cmd = 'test'
		}

		expected := read_out_file(file)
		res := os.exec('node ${$BAITEXE} --nocolor ${runner.cmd_options} ${cmd} ${file}')
		runner.compare_expected(res, expected)
	}
}

fun read_out_file(in_file_path string) string {
	out_file := in_file_path.replace('.in.bt', '.out')
	if not os.exists(out_file) {
		eprintln('out file ${out_file} does not exist')
		assert false
		return ''
	}

	return os.read_file(out_file)
}

fun normalize(s string) string {
	return s.replace('\\', '/').replace('\r\n', '\n')
}
